# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-TACTIC-BROADCAST.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    athena(n) TACTIC:BROADCAST order tests.
#
#    This test suite tests the BROADCAST tactic's orders.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests
    proc setup {} {
        ted create JOE BOB CBS CNN IOM1 IOM2
        variable S1
        variable B1
        variable T1
        set S1 [tdb strategy getname JOE]
        ted order STRATEGY:BLOCK:ADD agent JOE
        lassign [$S1 blocks] B1
        ted order BLOCK:TACTIC:ADD block_id [$B1 id] typename BROADCAST
        lassign [$B1 tactics] T1
    }


    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        ted cleanup
    }

    #-------------------------------------------------------------------
    # TACTIC:BROADCAST

    test TACTIC:BROADCAST-1.1 {required parms} -body {
        ted order -reject TACTIC:BROADCAST
    } -result {
        tactic_id {required value}
    }

    test TACTIC:BROADCAST-1.2 {unknown tactic_id} -body {
        ted order -reject TACTIC:BROADCAST tactic_id NONESUCH
    } -result {
        tactic_id {Invalid BROADCAST ID: "NONESUCH"}
    }

    test TACTIC:BROADCAST-1.3 {cost is invalid} -setup {
        setup
    } -body {
        ted order -reject TACTIC:BROADCAST \
            tactic_id [$T1 id] \
            cost NONESUCH
    } -cleanup {
        cleanup
    } -result {
        cost {invalid money value "NONESUCH", expected positive numeric value with optional K, M, or B suffix}
    }

    test TACTIC:BROADCAST-1.4 {cost is negative} -setup {
        setup
    } -body {
        ted order -reject TACTIC:BROADCAST \
            tactic_id [$T1 id] \
            cost -1
    } -cleanup {
        cleanup
    } -result {
        cost {invalid money value "-1", expected positive numeric value with optional K, M, or B suffix}
    }

    test TACTIC:BROADCAST-1.5 {invalid name} -setup {
        setup
    } -body {
        ted order -reject TACTIC:BROADCAST tactic_id [$T1 id] name 1234
    } -cleanup {
        cleanup
    } -match glob -result {
        name {Identifiers begin with a letter and contain only letters and digits.}
    }

    test TACTIC:BROADCAST-1.6 {duplicate name} -setup {
        setup
        set T2 [ted addtactic $B1 ABSIT]
    } -body {
        ted order -reject TACTIC:BROADCAST tactic_id [$T1 id] \
            name [$T2 get name]
    } -cleanup {
        cleanup
    } -match glob -result {
        name {Name already exists: "T2"}
    }

    test TACTIC:BROADCAST-2.1 {tactic updated} -setup {
        setup
    } -body {
        ted order TACTIC:BROADCAST \
            tactic_id [$T1 id] \
            a         SELF     \
            cap       CBS      \
            iom       IOM1     \
            cost      1000.0   

        ted pdicts [$T1 getdict]
    } -cleanup {
        cleanup
    } -match dictglob -result {
        a          SELF
        cap        CBS
        cost       1000.0
        execstatus NONE
        faildict   {}
        id         *
        iom        IOM1
        name       T*
        parent     *
        pot        *::pot
        state      invalid
    }

    test TACTIC:BROADCAST-2.2 {Notifier event} -setup {
        setup
    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        ted order TACTIC:BROADCAST \
            tactic_id [$T1 id] \
            cap       CBS      \
            iom       IOM1   

        ted notifier received

    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}


    test TACTIC:BROADCAST-3.1 {undo notifier event} -setup {
        setup
        ted order TACTIC:BROADCAST \
            tactic_id [$T1 id] \
            cap       CBS      \
            iom       IOM1   
    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        tdb order undo
        ted notifier received
    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}

    test TACTIC:BROADCAST-4.1 {identity} -setup {
        setup
    } -body {
        ted tactic identity $T1
    } -cleanup {
        cleanup
    } -result {OK}

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





