# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-sim.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    sim(sim) tests.
#
#    This test suite tests the sim(sim) mutators and queries
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Get initial time 0
    set clockdata [tdb clock checkpoint]

    variable tickList

    proc tickCmd {state i n} {
        variable tickList
        lappend tickList [list $state $i $n]

        assert {$state eq [tdb state]}
    }

    proc pauseCmd {state i n} {
        if {$state eq "RUNNING" && $i == 3} {
            tdb sim pause
        }
    }
    
    # Clean up after a test

    proc cleanup {} {
        variable clockdata

        # Clean up the test environment
        tdb clock restore $clockdata

        if {[tdb state] eq "RUNNING"} {
            tdb sim pause
        }

        if {[tdb state] eq "PAUSED"} {
            tdb sim unlock
        }

        ted cleanup
    }

    #-------------------------------------------------------------------
    # init
    #
    # Verify initial module state
   

    test init-1.1 {Initial state} -body {
        list \
            [tdb clock now]       \
            [tdb state]       \
            [tdb sim stoptime]    \
            [tdb clock asString]
        
    } -result {0 PREP 0 2012W01}

    #-------------------------------------------------------------------
    # startdate

    test startdate-1.1 {Sets the simclock's start date} -body {
        tdb sim startdate 2014W26
        tdb clock cget -week0
    } -cleanup {
        cleanup
    } -result {2014W26}

    
    test startdate-1.2 {Sends <Time>} -body {
        ted notifier bind ::tdb <Time>
        tdb sim startdate 2014W26
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::tdb <Time>}
    }


    test startdate-1.3 {Returns undo script} -body {
        tdb sim startdate 2014W26
    } -cleanup {
        cleanup
    } -match glob -result {* startdate 2012W01}


    test startdate-1.4 {Sets "changed"} -body {
        tdb sim startdate 2014W26
        tdb sim changed
    } -cleanup {
        cleanup
    } -result {1}

    #-------------------------------------------------------------------
    # starttick

    test starttick-1.1 {Sets the simclock's start tick} -body {
        tdb sim starttick 52
        tdb clock cget -tick0
    } -cleanup {
        cleanup
    } -result {52}

    
    test starttick-1.2 {Sends <Time>} -body {
        ted notifier bind ::tdb <Time>
        tdb sim starttick 52
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::tdb <Time>}
    }


    test starttick-1.3 {Returns undo script} -body {
        tdb sim starttick 52
    } -cleanup {
        cleanup
    } -match glob -result {* starttick 0}


    test starttick-1.4 {Sets "changed"} -body {
        tdb sim starttick 52
        tdb sim changed
    } -cleanup {
        cleanup
    } -result {1}

    #-------------------------------------------------------------------
    # lock

    test lock-1.1 {lock sets state to PAUSED} -setup {
        ted create NB1 BLUE SHIA
        tdb parm set econ.gdpExp 0
        tdb parm set econ.empExp 0
    } -body {
        tdb sim lock

        tdb state
    } -cleanup {
        cleanup
    } -result {PAUSED}


    test lock-1.2 {must be in PREP} -setup {
        ted create NB1 BLUE SHIA
        tdb parm set econ.gdpExp 0
        tdb parm set econ.empExp 0
        tdb sim lock
    } -body {
        tdb sim lock
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Assertion failed: $info(state) eq "PREP"}

    test lock-1.3 {Saves a PREP snapshot} -setup {
        ted create NB1 BLUE SHIA
        tdb parm set econ.gdpExp 0
        tdb parm set econ.empExp 0
    } -body {
        tdb sim lock

        tdb eval {SELECT tick FROM snapshots}
    } -cleanup {
        cleanup
    } -result {-1}

    test lock-1.4 {Initializes URAM} -setup {
        ted create NB1 BLUE SHIA
        tdb parm set econ.gdpExp 0
        tdb parm set econ.empExp 0
    } -body {
        tdb sim lock

        tdb onecolumn {SELECT n FROM uram_n}
    } -cleanup {
        cleanup
    } -result {NB1}

    #-------------------------------------------------------------------
    # run/pause

    test runpause-1.1 {Run for 1 week} -setup {
        ted create NB1 BLUE SHIA
        tdb parm set econ.gdpExp 0
        tdb parm set econ.empExp 0
    } -body {
        tdb sim lock
        tdb sim run
        tdb sim stoptime
    } -cleanup {
        cleanup
    } -result {1}

    test runpause-1.2 {Run for -ticks} -setup {
        ted create NB1 BLUE SHIA
        tdb parm set econ.gdpExp 0
        tdb parm set econ.empExp 0
    } -body {
        tdb sim lock
        tdb sim run -ticks 5
        tdb sim stoptime
    } -cleanup {
        cleanup
    } -result {5}

    test runpause-1.3 {Run -until} -setup {
        ted create NB1 BLUE SHIA
        tdb parm set econ.gdpExp 0
        tdb parm set econ.empExp 0
    } -body {
        tdb sim lock
        tdb sim run -until 10
        tdb sim stoptime
    } -cleanup {
        cleanup
    } -result {10}

    test runpause-1.4 {Can't run unless "PAUSED"} -setup {
        ted create NB1 BLUE SHIA
        tdb parm set econ.gdpExp 0
        tdb parm set econ.empExp 0
    } -body {
        tdb sim lock
        tdb sim SetState RUNNING
        tdb sim run
    } -returnCodes {
        error
    } -cleanup {
        tdb sim SetState PAUSED
        cleanup
    } -result {Assertion failed: $info(state) eq "PAUSED"}

    test runpause-1.5 {Calls tickcmd} -setup {
        ted create NB1 BLUE SHIA
        tdb parm set econ.gdpExp 0
        tdb parm set econ.empExp 0
    } -body {
        tdb sim lock
        tdb sim configure -tickcmd [namespace current]::tickCmd
        tdb sim run -ticks 2
        set tickList
    } -cleanup {
        tdb sim configure -tickcmd ""
        cleanup
    } -result {{RUNNING 1 2} {RUNNING 2 2} {PAUSED 2 2}}


    test runpause-1.5 {Sends Events} -setup {
        ted create NB1 BLUE SHIA
        tdb parm set econ.gdpExp 0
        tdb parm set econ.empExp 0
    } -body {
        tdb sim lock
        ted notifier bind ::tdb <Tick>
        ted notifier bind ::tdb <State>
        tdb sim run -ticks 2
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::tdb <State>}
        {::tdb <Tick>}
        {::tdb <Tick>}
        {::tdb <State>}
    }


    test runpause-1.6 {Returns undo script} -setup {
        ted create NB1 BLUE SHIA
        tdb parm set econ.gdpExp 0
        tdb parm set econ.empExp 0
    } -body {
        tdb sim lock
        tdb sim run
    } -cleanup {
        cleanup
    } -result {}


    test runpause-2.1 {Pauses if running} -setup {
        ted create NB1 BLUE SHIA
        tdb parm set econ.gdpExp 0
        tdb parm set econ.empExp 0
    } -body {
        tdb sim lock
        tdb sim configure -tickcmd [namespace current]::pauseCmd
        tdb sim run -ticks 10
        list [tdb state] [tdb clock now]
    } -cleanup {
        cleanup
    } -result {PAUSED 3}
    
    test runpause-2.2 {Returns undo script} -setup {
        ted create NB1 BLUE SHIA
    } -body {
        tdb sim pause
    } -cleanup {
        cleanup
    } -result {}

    #-------------------------------------------------------------------
    # unlock

    test unlock-1.1 {Can't unlock in PREP} -body {
        tdb sim unlock
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Assertion failed: $info(state) eq "PAUSED"}

    test unlock-1.2 {Sets state to PREP} -setup {
        ted create NB1 BLUE SHIA
        tdb parm set econ.gdpExp 0
        tdb parm set econ.empExp 0
        tdb sim lock
    } -body {
        tdb sim unlock
        tdb state
    } -cleanup {
        cleanup
    } -result {PREP}

    test unlock-1.3 {Purges snapshot} -setup {
        ted create NB1 BLUE SHIA
        tdb parm set econ.gdpExp 0
        tdb parm set econ.empExp 0
        tdb sim lock
        tdb sim run -ticks 1
    } -body {
        tdb sim unlock
        tdb eval {SELECT tick FROM snapshots}
    } -cleanup {
        cleanup
    } -result {}

    #-------------------------------------------------------------------
    # state

    test state-1.1 {Returns current state} -setup {
        ted create NB1 BLUE SHIA
        tdb parm set econ.gdpExp 0
        tdb parm set econ.empExp 0
    } -body {
        set states [list]
        
        lappend states [tdb state]
        tdb sim lock
        lappend states [tdb state]

        set states
    } -cleanup {
        cleanup
    } -result {PREP PAUSED}


    #-------------------------------------------------------------------
    # restart
    #
    # NOTE: sim unlock is tested pretty thoroughly during the "cleanup"
    # process and by the other tests.

    test restart-1.1 {Check results} -setup {
        ted create NB1 BLUE SHIA
        tdb parm set econ.gdpExp 0
        tdb parm set econ.empExp 0
        tdb sim lock
        tdb sim run -ticks 5
        tdb sim startdate 2014W26
    } -body {
        tdb sim unlock

        list \
            [tdb state]         \
            [tdb clock now]         \
            [tdb sim changed]       \
            [tdb clock cget -week0]
    } -cleanup {
        cleanup
    } -result {PREP 0 1 2012W01}
    
    #-------------------------------------------------------------------
    # rebase
    #
    # Note: this tests [tdb sim rebase] specifically; more general
    # rebase testing is done in 010-rebase.test.
    
    test rebase-1.1 {in prep after rebase} -setup {
        ted create NB1 BLUE SHIA
        ted lock
        ted step
        ted step
    } -body {
        tdb sim rebase
        list \
            [tdb state] \
            [tdb clock now] \
            [tdb clock cget -tick0]
    } -cleanup {
        cleanup
    } -result {PREP 2 2}
        

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::






