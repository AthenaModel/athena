# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-aam.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    aam(sim) tests.
#
#    This test suite tests the aam(sim) mutators and queries
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Clean up after a test

    proc pprint_demog_g {} {
        ted query {
            SELECT g, attrition, population
            FROM demog_g
        }
    }

    # Units
    variable units
    array set units {
        BLUE-NB1/0000 {
            g         BLUE
            n         NB1
            a         NONE
            personnel 15
            tactic_id 0
        }

        BLUE-NB1/0001 {
            g         BLUE
            n         NB1
            a         PATROL
            personnel 15
            tactic_id 1
        }

        BRIT-NB1/0000 {
            g         BRIT
            n         NB1
            a         NONE
            personnel 15
            tactic_id 0
        }

        USAID-NB1/0000 {
            g         USAID
            n         NB1
            a         NONE
            personnel 15
            tactic_id 0
        }

        USAID-NB1/0001 {
            g         USAID
            n         NB1
            a         CMO_HEALTHCARE
            personnel 15
            tactic_id 1
        }

        HAL-NB1/0000 {
            g         HAL
            n         NB1
            a         NONE
            personnel 15
            tactic_id 0
        }
    }

    # Call this after creating all neighborhoods and units.
    proc mkunits {args} {
        variable units

        tdb personnel start

        tdb eval { DELETE FROM units }

        foreach unit $args {
            dict with units($unit) {
                tdb eval {
                    INSERT INTO units(u,g,n,a,personnel)
                    VALUES($unit,$g,$n,'NONE',$personnel);
                }
            }
        }
    }

    proc cleanup {} {
        # Clean up the test environment
        ted cleanup
    }


    #-------------------------------------------------------------------
    # attrit

    # 1.* -- Attrition to FRC and ORG units.
    #
    # The code is identical for both types, so we do one ORG test just 
    # to show that we can do it.

    test attrit-1.1 {f is FRC, all BLUE units attrited} -setup {
        ted create NB1 BLUE BRIT
        mkunits BLUE-NB1/0000 BLUE-NB1/0001 BRIT-NB1/0000
    } -body {
        tdb aam attrit [list mode GROUP n NB1 f BLUE casualties 20 g1 "" g2 ""]
        tdb aam assess
        # All units had 15 personnel to start.
        # The two BLUE units should be attrited equally.
        # The BRIT unit should not be touched.
        ted query {SELECT u,personnel FROM units}
    } -cleanup {
        cleanup
    } -result {
u             personnel 
------------- --------- 
BLUE-NB1/0000 5         
BLUE-NB1/0001 5         
BRIT-NB1/0000 15        
    }


    test attrit-1.2 {f is FRC, overkill leaves 0} -setup {
        ted create NB1 BLUE BRIT
        mkunits BLUE-NB1/0000 BLUE-NB1/0001 BRIT-NB1/0000
    } -body {
        tdb aam attrit [list mode GROUP n NB1 f BLUE casualties 40 g1 "" g2 ""]

        tdb aam assess
        # All BLUE units should be at 0.
        ted query {SELECT u,personnel FROM units}
    } -cleanup {
        cleanup
    } -result {
u             personnel 
------------- --------- 
BLUE-NB1/0000 0         
BLUE-NB1/0001 0         
BRIT-NB1/0000 15        
    }


    test attrit-1.3 {f is FRC, no units present} -setup {
        ted create NB1 BLUE BRIT
        mkunits BRIT-NB1/0000
    } -body {
        tdb aam attrit [list mode GROUP n NB1 f BLUE casualties 40 g1 "" g2 ""]

        tdb aam assess
        # All BLUE units should be at 0.
        ted query {SELECT u,personnel FROM units}
    } -cleanup {
        cleanup
    } -result {
u             personnel 
------------- --------- 
BRIT-NB1/0000 15        
    }

    
    test attrit-1.4 {f is ORG, units attrited in proper order} -setup {
        ted create NB1 USAID HAL
        mkunits USAID-NB1/0000 USAID-NB1/0001 HAL-NB1/0000
    } -body {
        tdb aam attrit [list mode GROUP n NB1 f USAID casualties 20 g1 "" g2 ""]
        tdb aam assess
        # All units had 15 personnel to start.
        # The USAID units should be attrited equally.
        # The HAL unit should not be touched.
        ted query {SELECT u,personnel FROM units}
    } -cleanup {
        cleanup
    } -result {
u              personnel 
-------------- --------- 
USAID-NB1/0000 5         
USAID-NB1/0001 5         
HAL-NB1/0000   15        
    }


    # 2.* -- Attrition to a particular civ group

    test attrit-2.1 {f is CIV, not resident in n} -setup {
        ted create NB1 NB2 BLUE SHIA KURD 
        ted lock
    } -body {
        tdb aam attrit [list mode GROUP n NB1 f KURD casualties 20 g1 "" g2 ""]
        tdb aam assess
        tdb demog stats

        # No error
        pprint_demog_g
    } -cleanup {
        cleanup
    } -result {
g    attrition population 
---- --------- ---------- 
SHIA 0         1000       
KURD 0         1000       
    }

    test attrit-2.2 {f is CIV, no units in n, normal} -setup {
        ted create NB1 BLUE SHIA SUNN
        ted lock
    } -body {
        tdb aam attrit [list mode GROUP n NB1 f SHIA casualties 20 g1 "" g2 ""]
        tdb aam assess
        tdb demog stats

        pprint_demog_g
    } -cleanup {
        cleanup
    } -result {
g    attrition population 
---- --------- ---------- 
SHIA 20        980        
SUNN 0         1000       
    }


    test attrit-2.3 {f is CIV, resident in n, overkill leaves 0} -setup {
        ted create NB1 BLUE SHIA SUNN
        ted lock
    } -body {
        tdb aam attrit [list mode GROUP n NB1 f SHIA casualties 2000 g1 "" g2 ""]
        tdb aam assess
        tdb demog stats

        pprint_demog_g
    } -cleanup {
        cleanup
    } -result {
g    attrition population 
---- --------- ---------- 
SHIA 1000      0          
SUNN 0         1000       
    }


    test attrit-2.5 {f is CIV, overkill, attrition is saved} -setup {
        ted create NB1 BLUE SHIA SUNN
        ted lock
    } -body {
        # There are only 1000 in the group, so we should get 1000
        # casualties.
        tdb aam attrit [list mode GROUP n NB1 f SHIA casualties 2000 g1 "" g2 ""]
        
        tdb aam assess
        ted query {SELECT u,personnel FROM units}
    } -cleanup {
        cleanup
    } -result {
u        personnel 
-------- --------- 
SHIA/NB1 0         
SUNN/NB1 1000      
    }

    test attrit-2.6 {f is CIV, overkill, g1, g2 are saved} -setup {
        ted create NB1 BLUE BRIT SHIA SUNN
        ted lock
    } -body {
        tdb aam attrit \
            [list mode GROUP n NB1 f SHIA casualties 2000 g1 BLUE g2 BRIT]
        
        tdb aam assess
        ted query {SELECT u,personnel FROM units}
    } -cleanup {
        cleanup
    } -result {
u        personnel 
-------- --------- 
SHIA/NB1 0         
SUNN/NB1 1000      
    }

    test attrit-2.8 {f is empty CIV group} -setup {
        ted create NB1 BLUE SHIA SUNN
        ted lock
        tdb aam attrit [list mode GROUP n NB1 f SHIA casualties 2000 g1 "" g2 ""]
        tdb aam assess
    } -body {
        # Attriting an empty civilian group causes no problems.
        tdb aam attrit [list mode GROUP n NB1 f SHIA casualties 2000 g1 "" g2 ""]
        tdb aam assess
        tdb demog stats

        pprint_demog_g
    } -cleanup {
        cleanup
    } -result {
g    attrition population 
---- --------- ---------- 
SHIA 1000      0          
SUNN 0         1000       
    }  


    test attrit-3.1 {attrits deploy_tng} -setup {
        ted create NB1 BLUE BRIT SHIA
        ted deploy NB1 BLUE 100
        ted deploy NB1 BLUE 200
        ted deploy NB1 BRIT 200
        ted lock
    } -body {
        tdb aam attrit {
            mode        GROUP 
            n           NB1 
            f           BLUE 
            casualties  150 
            g1          "" 
            g2          ""
        }
        tdb aam ApplyAttrition
        ted query {select n,g,personnel from deploy_tng}
    } -cleanup {
        cleanup
    } -match glob -result {
n   g    personnel 
--- ---- --------- 
NB1 BLUE 50        
NB1 BLUE 100       
NB1 BRIT 200       
    }
    
    
    #-------------------------------------------------------------------
    # attrit neighborhood


    # 1.* -- All civilians in nbhood.

    test attritn-1.1 {All civilians in nbhood} -setup {
        ted create NB1 BLUE SHIA SUNN
        ted lock
    } -body {
        tdb aam attrit [list mode NBHOOD n NB1 casualties 20 g1 "" g2 ""]
        tdb aam assess
        tdb demog stats

        pprint_demog_g
    } -cleanup {
        cleanup
    } -result {
g    attrition population 
---- --------- ---------- 
SHIA 10        990        
SUNN 10        990        
    }


    test attritn-1.2 {nbhood, overkill leaves 0 per resident} -setup {
        ted create NB1 BLUE SHIA SUNN
        ted lock
    } -body {
        tdb aam attrit [list mode NBHOOD n NB1 casualties 3000  g1 "" g2 ""]
        tdb aam assess
        tdb demog stats

        pprint_demog_g
    } -cleanup {
        cleanup
    } -result {
g    attrition population 
---- --------- ---------- 
SHIA 1000      0          
SUNN 1000      0          
    }


    test attritn-1.3 {nbhood, attrition is proportional} -setup {
        ted create NB1 BLUE SHIA SUNN
        ted lock
    } -body {
        tdb aam attrit [list mode NBHOOD n NB1 casualties 100  g1 "" g2 ""]
        tdb aam assess
        tdb demog stats

        set a [pprint_demog_g]
        set b [ted query {SELECT u,personnel FROM units}]
        set c "$a$b"
    } -cleanup {
        cleanup
    } -result {
g    attrition population 
---- --------- ---------- 
SHIA 50        950        
SUNN 50        950        
    
u        personnel 
-------- --------- 
SHIA/NB1 950       
SUNN/NB1 950       
    }

    # attritn-1.4 deleted, obsolete

    test attritn-1.5 {nbhood, force, org units not attrited} -setup {
        ted create JOE NB1 BLUE USAID BRIT SHIA SUNN
        ted deploy NB1 BLUE 15
        ted deploy NB1 USAID 15
        ted deploy NB1 BRIT 15
        ted lock
    } -body {
        tdb aam attrit [list mode NBHOOD n NB1 casualties 10000 g1 BLUE g2 BRIT]
        tdb aam assess

        ted query {SELECT u,personnel FROM units ORDER BY u}
    } -cleanup {
        cleanup
    } -result {
u         personnel 
--------- --------- 
BLUE/NB1  15        
BRIT/NB1  15        
SHIA/NB1  0         
SUNN/NB1  0         
USAID/NB1 15        
    }


    test attritn-1.7 {attriting empty neighborhood is OK} -setup {
        ted create NB1 BLUE SHIA SUNN
        ted lock
        tdb aam attrit [list mode NBHOOD n NB1 casualties 3000  g1 "" g2 ""]
        tdb aam assess
    } -body {
        tdb aam attrit [list mode NBHOOD n NB1 casualties 3000  g1 "" g2 ""]
        tdb aam assess
        tdb demog stats

        pprint_demog_g
    } -cleanup {
        cleanup
    } -result {
g    attrition population 
---- --------- ---------- 
SHIA 1000      0          
SUNN 1000      0          
    }

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::






