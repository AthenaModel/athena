# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-driver.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    mad(sim) tests.
#
#    This test suite tests the driver(sim) module.
#
#    Note: the driver type definition calls are adequately tested by the
#    040-* rule sets and will not be tested here.
#
# TBD: This actually tests ruleset_manager.tcl, which has replaced
# driver.tcl.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        ted cleanup
    }

    # Define an htools buffer for tests
    htools ht

    # pprint text
    #
    # text  - block text
    #
    # Prints it so that it looks nice in a -result option.

    proc pprint {text} {
        return "\n[string trim $text]\n    "
    }


    #-------------------------------------------------------------------
    # getid

    test getid-1.1 {Drivers can be created} -body {
        tdb ruleset getid ACCIDENT {n NB1}
        tdb ruleset getid PATROL {n NB1 g BLUE}

        ted querylist {SELECT * FROM drivers}
    } -cleanup {
        cleanup
    } -result {
driver_id  1000
dtype      ACCIDENT
signature  NB1

driver_id  1001
dtype      PATROL
signature  NB1 BLUE
    }

    test getid-1.2 {Returns driver ID} -body {
        tdb ruleset getid ACCIDENT {n NB1}
    } -cleanup {
        cleanup
    } -result {1000}

    test getid-1.3 {Retrieves existing id} -body {
        set a [tdb ruleset getid ACCIDENT {n NB1}]
        set b [tdb ruleset getid ACCIDENT {n NB1}]
        expr {$a == $b}
    } -cleanup {
        cleanup
    } -result {1}

    #-------------------------------------------------------------------
    # get

    test get-1.1 {Retrieves existing id} -setup {
        set a [tdb ruleset getid ACCIDENT {n NB1}]
    } -body {
        set b [tdb ruleset get ACCIDENT NB1]
        expr {$a == $b}
    } -cleanup {
        cleanup
    } -result {1}

    test get-1.2 {Returns "" if no such driver} -setup {
        tdb ruleset getid ACCIDENT {n NB1}
    } -body {
        tdb ruleset get RIOT NB1
    } -cleanup {
        cleanup
    } -result {}

    #-------------------------------------------------------------------
    # rulename

    test rulename-1.1 {Retrieves full rule name} -body {
        tdb ruleset rulename CIVCAS-1-1
    } -result {Civilian casualties taken}

    #-------------------------------------------------------------------
    # narrative

    test narrative-1.1 {narrative output: CIVCAS-1} -body {
        tdb ruleset narrative CIVCAS {
            dtype      CIVCAS
            f          SHIA
            casualties 100
        } 
    } -result {{group:SHIA} took 100 casualties}

    #-------------------------------------------------------------------
    # detail

    test detail-1.1 {detail output: CIVCAS-1} -body {
        tdb ruleset detail CIVCAS {
            dtype      CIVCAS
            f          SHIA
            casualties 100
            mult       2.0
        } [namespace current]::ht

        pprint [ht get] 
    } -result {
Civilian group <a href="my://app/group/SHIA">SHIA</a>
took a total of 100 casualties this week
from incidents of all kinds.
The Z-curve multiplier is <i>mult</i>=2.00.<p>
    }

    

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





