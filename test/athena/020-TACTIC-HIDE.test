# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-TACTIC-HIDE.test
#
# AUTHOR:
#    Dave Hanks
#
# DESCRIPTION:
#    TACTIC:HIDE:* Orders tests
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    proc setup {} {
        variable B1
        variable T1

        ted create JOE BLUE ALQ NB1 NB2 SUNN SHIA KURD USAID

        set B1 [ted addblock SYSTEM]
        set T1 [ted addtactic $B1 HIDE]
    }

    # Clean up after a test

    proc cleanup {} {
        ted cleanup
    }

    #-------------------------------------------------------------------
    # TACTIC:HIDE

    test TACTIC:HIDE-1.1 {required parameters} -setup {
        setup
    } -body {
        ted order -reject TACTIC:HIDE
    } -cleanup {
        cleanup
    } -result {
        tactic_id {required value}
    }

    test TACTIC:HIDE-1.2 {unknown tactic_id} -setup {
        setup
    } -body {
        ted order -reject TACTIC:HIDE tactic_id NONESUCH
    } -cleanup {
        cleanup
    } -match glob -result {
        tactic_id {Invalid HIDE ID: "NONESUCH"}
    }

    test TACTIC:HIDE-1.3 {invalid name} -setup {
        setup
    } -body {
        ted order -reject TACTIC:HIDE tactic_id [$T1 id] name 1234
    } -cleanup {
        cleanup
    } -match glob -result {
        name {Identifiers begin with a letter and contain only letters and digits.}
    }

    test TACTIC:HIDE-1.4 {duplicate name} -setup {
        setup
        set T2 [ted addtactic $B1 ABSIT]
    } -body {
        ted order -reject TACTIC:HIDE tactic_id [$T1 id] \
            name [$T2 get name]
    } -cleanup {
        cleanup
    } -match glob -result {
        name {Name already exists: "T2"}
    }

    test TACTIC:HIDE-2.1 {tactic valid} -setup {
        setup
    } -body {
        ted order TACTIC:HIDE \
            tactic_id [$T1 id]  \
            f          BLUE     \
            nlist     [tdb gofer make NBHOODS BY_VALUE {NB1}]

        ted pdicts [$T1 getdict]
    } -cleanup {
        cleanup
    } -match dictglob -result {
        execstatus NONE
        f          BLUE
        faildict   {}
        id         *
        name       T*
        nlist      {_type NBHOODS _rule BY_VALUE nlist NB1}
        parent     *
        pot        *::pot
        state      invalid
    }

    test TACTIC:HIDE-2.2 {notifier event} -setup {
        setup
    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        ted order TACTIC:HIDE \
            tactic_id [$T1 id]  \
            f          BLUE     \
            nlist     [tdb gofer make NBHOODS BY_VALUE {NB1}] 

        ted notifier received
    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}

    test TACTIC:HIDE-2.3 {undo notifier event} -setup {
        setup

        ted order TACTIC:HIDE \
            tactic_id [$T1 id]  \
            f          BLUE     \
            nlist     [tdb gofer make NBHOODS BY_VALUE {NB1}]

    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        tdb order undo
        ted notifier received
    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}
 
    test TACTIC:HIDE-3.1 {identity} -setup {
        setup
    } -body {
        ted tactic identity $T1
    } -cleanup {
        cleanup
    } -result {OK}

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::

