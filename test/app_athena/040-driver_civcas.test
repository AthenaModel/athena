# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    040-driver_civcas.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    CIVCAS rules tests
#
#    This test suite tests that the rules fire when appropriate.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Define an htools buffer for tests
    htools ht

    # pprint text
    #
    # text  - block text
    #
    # Prints it so that it looks nice in a -result option.

    proc pprint {text} {
        return "\n[string trim $text]\n    "
    }

    # setup
    #
    # Sets up for tests.
    
    proc setup {} {
        # FIRST, other rule sets shouldn't muddy the waters.
        tdb parm set dam.CONSUMP.active no
        tdb parm set dam.UNEMP.active no
        tdb parm set dam.ENI.active no
        tdb parm set dam.ENERGY.active no
        tdb parm set dam.WATER.active no
        tdb parm set dam.TRANSPORT.active no
        adb eval {DELETE FROM drivers}
        
        # NEXT, create the entities
        ted create JOE BOB NB1 BLUE BRIT SHIA SUNN
        
        # NEXT, lock, so we can do magic attrition
        ted lock
    }
    
    # attrit casualties ?g1 ?g2??
    #
    # casualties  - The number of casualities to inflict
    # g1          - The first responsible force group, if any
    # g2          - The second responsible force group, if any.
    #
    # Attrits groups in NB1, and advances time so that rules fire.
    
    proc attrit {casualties {g1 ""} {g2 ""}} {
        aam attrit [dict create            \
                    mode       NBHOOD      \
                    n          NB1         \
                    f          ""          \
                    casualties $casualties \
                    g1        $g1          \
                    g2        $g2]
        ted step
    }
    
    # Clean up after a test
    proc cleanup {} {
        ht clear
        ted cleanup
    }

    #-------------------------------------------------------------------
    # assess

    test assess-1.1 {Rule 1-1 fires} -setup {
        setup
    } -body {
        attrit 1000
        ted querylist {
            SELECT * FROM rule_firings WHERE rule GLOB 'CIVCAS-1-*'
        }
    } -cleanup {
        cleanup
    } -result {
firing_id  1
t          1
driver_id  1000
ruleset    CIVCAS
rule       CIVCAS-1-1
fdict      casualties 500 f SHIA dtype CIVCAS mult 2.0

firing_id  2
t          1
driver_id  1001
ruleset    CIVCAS
rule       CIVCAS-1-1
fdict      casualties 500 f SUNN dtype CIVCAS mult 2.0
    }
    
    test assess-1.2 {Rule 1-1 inputs} -setup {
        setup
    } -body {
        attrit 1000
        ted query {
            SELECT rule, g, c, note 
            FROM rule_firings JOIN rule_inputs USING (firing_id)
            WHERE rule GLOB 'CIVCAS-1-*'
        }
    } -cleanup {
        cleanup
    } -result {
rule       g    c   note 
---------- ---- --- ---- 
CIVCAS-1-1 SHIA AUT      
CIVCAS-1-1 SHIA SFT      
CIVCAS-1-1 SHIA QOL      
CIVCAS-1-1 SUNN AUT      
CIVCAS-1-1 SUNN SFT      
CIVCAS-1-1 SUNN QOL      
    }
    
    test assess-2.1 {Rule 2-1 does not fire without groups} -setup {
        setup
    } -body {
        attrit 1000
        adb eval {
            SELECT count(*) FROM rule_firings WHERE rule GLOB 'CIVCAS-2-*'
        }
    } -cleanup {
        cleanup
    } -result {0}
    
    test assess-2.2 {Rule 2-1 fires with one group} -setup {
        setup
    } -body {
        attrit 1000 BLUE
        ted querylist {
            SELECT * FROM rule_firings WHERE rule GLOB 'CIVCAS-2-*'
        }
    } -cleanup {
        cleanup
    } -result {
firing_id  3
t          1
driver_id  1000
ruleset    CIVCAS
rule       CIVCAS-2-1
fdict      casualties 500 f SHIA dtype CIVCAS g BLUE mult 2.0

firing_id  4
t          1
driver_id  1001
ruleset    CIVCAS
rule       CIVCAS-2-1
fdict      casualties 500 f SUNN dtype CIVCAS g BLUE mult 2.0
    }
    
    test assess-2.3 {Rule 2-1 fires with two groups} -setup {
        setup
    } -body {
        attrit 1000 BLUE BRIT
        ted querylist {
            SELECT * FROM rule_firings WHERE rule GLOB 'CIVCAS-2-*'
        }
    } -cleanup {
        cleanup
    } -result {
firing_id  3
t          1
driver_id  1000
ruleset    CIVCAS
rule       CIVCAS-2-1
fdict      casualties 500 f SHIA dtype CIVCAS g BLUE mult 2.0

firing_id  4
t          1
driver_id  1000
ruleset    CIVCAS
rule       CIVCAS-2-1
fdict      casualties 500 f SHIA dtype CIVCAS g BRIT mult 2.0

firing_id  5
t          1
driver_id  1001
ruleset    CIVCAS
rule       CIVCAS-2-1
fdict      casualties 500 f SUNN dtype CIVCAS g BLUE mult 2.0

firing_id  6
t          1
driver_id  1001
ruleset    CIVCAS
rule       CIVCAS-2-1
fdict      casualties 500 f SUNN dtype CIVCAS g BRIT mult 2.0
    }
    
    
    test assess-2.4 {Rule 2-1 inputs} -setup {
        setup
    } -body {
        attrit 1000 BLUE
        ted query {
            SELECT rule, f, g, note 
            FROM rule_firings JOIN rule_inputs USING (firing_id)
            WHERE rule GLOB 'CIVCAS-2-*'
        }
    } -cleanup {
        cleanup
    } -result {
rule       f    g    note 
---------- ---- ---- ---- 
CIVCAS-2-1 SHIA BLUE      
CIVCAS-2-1 SUNN BLUE      
    }
    
    
    #-------------------------------------------------------------------
    # sigline

    test sigline-1.1 {sigline output} -setup {
        setup
    } -body {
        tdb ruleset CIVCAS sigline SHIA 
    } -cleanup {
        cleanup
    } -result {Casualties to group SHIA}
         

    #-------------------------------------------------------------------
    # narrative

    test narrative-1.1 {narrative output: CIVCAS-1} -setup {
        setup
    } -body {
        tdb ruleset CIVCAS narrative {
            dtype      CIVCAS
            f          SHIA
            casualties 100
        } 
    } -cleanup {
        cleanup
    } -result {{group:SHIA} took 100 casualties}

    test narrative-1.2 {narrative output: CIVCAS-2} -setup {
        setup
    } -body {
        tdb ruleset CIVCAS narrative {
            dtype      CIVCAS
            f          SHIA
            g          BLUE
            casualties 50
        } 
    } -cleanup {
        cleanup
    } -result {{group:SHIA} took 50 casualties from {group:BLUE}}
         
    #-------------------------------------------------------------------
    # detail

    test detail-1.1 {detail, CIVCAS-1} -setup {
        setup
    } -body {
        tdb ruleset CIVCAS detail {
            dtype      CIVCAS
            f          SHIA
            casualties 100
            mult       2.0
        } [namespace current]::ht

        pprint [ht get] 
    } -cleanup {
        cleanup
    } -result {
Civilian group <a href="my://app/group/SHIA">SHIA</a>
took a total of 100 casualties this week
from incidents of all kinds.
The Z-curve multiplier is <i>mult</i>=2.00.<p>
    }
    
    test detail-1.2 {detail, CIVCAS-2} -setup {
        setup
    } -body {
        tdb ruleset CIVCAS detail {
            dtype      CIVCAS
            f          SHIA
            g          BLUE
            casualties 50
            mult       1.5
        } [namespace current]::ht

        pprint [ht get] 
    } -cleanup {
        cleanup
    } -result {
Civilian group <a href="my://app/group/SHIA">SHIA</a>
took a total of 50 casualties this week
as collateral damage in incidents in which
<a href="my://app/group/BLUE">BLUE</a>
was involved.
The Z-curve multiplier is <i>mult</i>=1.50.<p>
    }
    
    
    cleanupTests
}

namespace delete ::athena_test::tests::









