# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-CONDITION-CONTROL.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    CONDITION:CONTROL:* Orders tests
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    proc setup {} {
        ted create NB1 NB2 JOE BOB BLUE SHIA SUNN KURD
        variable B1
        variable C1
        set B1 [ted addblock SYSTEM]
        set C1 [ted addcondition $B1 CONTROL]
    }

    # Clean up after a test

    proc cleanup {} {
        ted cleanup
    }

    #-------------------------------------------------------------------
    # CONDITION:CONTROL

    test CONDITION:CONTROL-1.1 {required parameters} -setup {
        setup
    } -body {
        ted order -reject CONDITION:CONTROL
    } -cleanup {
        cleanup
    } -result {
        condition_id {required value}
    }

    test CONDITION:CONTROL-1.2 {unknown condition_id} -setup {
        setup
    } -body {
        ted order -reject CONDITION:CONTROL condition_id NONESUCH
    } -cleanup {
        cleanup
    } -match glob -result {
        condition_id {Invalid CONTROL ID: "NONESUCH"}
    }

    test CONDITION:CONTROL-1.3 {invalid a} -setup {
        setup
    } -body {
        ted order -reject CONDITION:CONTROL condition_id [$C1 id] a NONESUCH
    } -cleanup {
        cleanup
    } -match glob -result {
        a {Invalid actor, should be one of: BOB, JOE}
    }

    test CONDITION:CONTROL-1.4 {invalid sense} -setup {
        setup
    } -body {
        ted order -reject CONDITION:CONTROL condition_id [$C1 id] \
            sense NONESUCH
    } -cleanup {
        cleanup
    } -match glob -result {
        sense {invalid value "NONESUCH", should be one of: DOES, DOESNT}
    }

    test CONDITION:CONTROL-1.5 {invalid anyall} -setup {
        setup
    } -body {
        ted order -reject CONDITION:CONTROL condition_id [$C1 id] \
            anyall NONESUCH
    } -cleanup {
        cleanup
    } -match glob -result {
        anyall {invalid value "NONESUCH", should be one of: ANY, ALL}
    }

    test CONDITION:CONTROL-1.6 {invalid name} -setup {
        setup
    } -body {
        ted order -reject CONDITION:CONTROL condition_id [$C1 id] name 1234
    } -cleanup {
        cleanup
    } -match glob -result {
        name {Identifiers begin with a letter and contain only letters and digits.}
    }

    test CONDITION:CONTROL-1.7 {duplicate name} -setup {
        setup
        set C2 [ted addcondition $B1 COMPARE]
    } -body {
        ted order -reject CONDITION:CONTROL condition_id [$C1 id] \
            name [$C2 get name]
    } -cleanup {
        cleanup
    } -match glob -result {
        name {Name already exists as condition: "C2"}
    }

    test CONDITION:CONTROL-2.1 {condition updated} -setup {
        setup
    } -body {
        ted order CONDITION:CONTROL \
            condition_id [$C1 id] \
            a      JOE  \
            sense  DOESNT \
            anyall ANY \
            nlist  [adb gofer make NBHOODS BY_VALUE {NB1 NB2}]

        ted pdicts [$C1 getdict]
    } -cleanup {
        cleanup
    } -match dictglob -result {
        a       JOE
        anyall  ANY
        id      *
        metflag {}
        name    C*
        nlist   {_type NBHOODS _rule BY_VALUE nlist {NB1 NB2}}
        parent  *
        pot     *::pot
        sense   DOESNT
        state   normal
    }

    test CONDITION:CONTROL-2.2 {sanity check done} -setup {
        setup
    } -body {
        ted order CONDITION:CONTROL condition_id [$C1 id] a "JOE" nlist NONESUCH
        ted pdicts [$C1 getdict]
    } -cleanup {
        cleanup
    } -match dictglob -result {
        a       JOE
        anyall  ALL
        id      *
        metflag {}
        name    C*
        nlist   NONESUCH
        parent  *
        pot     *::pot
        sense   DOES
        state   invalid
    }

    test CONDITION:CONTROL-2.3 {notifier event} -setup {
        setup
    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        ted order CONDITION:CONTROL condition_id [$C1 id] a JOE
        ted notifier received
    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}

    test CONDITION:CONTROL-2.4 {undo notifier event} -setup {
        setup
        ted order CONDITION:CONTROL condition_id [$C1 id] a JOE
    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        flunky undo
        ted notifier received
    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}
 
    test CONDITION:CONTROL-3.1 {identity} -setup {
        setup
    } -body {
        ted condition identity $C1
    } -cleanup {
        cleanup
    } -result {OK}

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





