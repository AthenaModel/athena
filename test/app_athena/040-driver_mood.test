# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    040-driver_mood.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    driver_mood(sim) tests
#
#    This test suite tests that the rules fire when appropriate.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Define an htools buffer for tests
    htools ht

    # pprint text
    #
    # text  - block text
    #
    # Prints it so that it looks nice in a -result option.

    proc pprint {text} {
        return "\n[string trim $text]\n    "
    }

    # setup delta
    #
    # delta   - Satisfaction delta for SHIA
    #
    # Sets up for tests.
    
    proc setup {delta} {
        # FIRST, other rule sets shouldn't muddy the waters.
        parm set dam.CONSUMP.active no
        parm set dam.PRESENCE.active no
        parm set dam.UNEMP.active no
        parm set dam.ENI.active no
        rdb eval {DELETE FROM drivers}
        rdb eval {DELETE FROM mads}
        
        # NEXT, create the entities
        ted create JOE BOB DAVE NB1 NB2 BLUE BRIT TAL SHIA SUNN KURD
        
        # NEXT, give JOE control of NB1.
        ted order NBHOOD:UPDATE n NB1 controller JOE
        
        # NEXT, lock, so can do magic inputs to affect mood.
        ted lock
        
        # NEXT, add an input of the relevant kind.
        ted order MAD:CREATE narrative "Mood Test" \
            cause UNIQUE s 0.0 p 0.0 q 0.0
        set madId [ted sendex last_mad]
        foreach c {AUT CUL QOL SFT} {
            ted order MAD:SAT mad_id $madId mode persistent \
                g SHIA c $c mag $delta
        }
        
        # NEXT, take a step, so that the inputs will do their thing.
        ted step
    }

    # Clean up after a test
    proc cleanup {} {
        ht clear
        ted cleanup
    }

    #-------------------------------------------------------------------
    # assess
    
    test assess-1.1 {Rule 1-1 fires} -setup {
        setup -100.0
    } -body {
        # Take one more step; this should cause the MOOD rule set to
        # fire.
        ted step
        ted querylist {
            SELECT * FROM rule_firings where ruleset='MOOD'
        }
    } -cleanup {
        cleanup
    } -result {
firing_id  5
t          2
driver_id  1001
ruleset    MOOD
rule       MOOD-1-1
fdict      controller JOE delta -100.0 moodThen 0.0 n NB1 dtype MOOD tc 0 moodNow -100.0 g SHIA
    }
    
    test assess-1.2 {Rule 1-1 inputs} -setup {
        setup -100.0
    } -body {
        # Take one more step; this should cause the MOOD rule set to
        # fire.
        ted step
        ted query {
            SELECT rule, g, a, note
            FROM rule_firings JOIN rule_inputs USING (firing_id)
            WHERE ruleset='MOOD'
            ORDER BY g,a
        }
    } -cleanup {
        cleanup
    } -result {
rule     g    a    note        
-------- ---- ---- ----------- 
MOOD-1-1 SHIA BOB  no control  
MOOD-1-1 SHIA DAVE no control  
MOOD-1-1 SHIA JOE  has control 
    }
    
    
    test assess-2.1 {Rule 1-2 fires} -setup {
        setup 100.0
    } -body {
        # Take one more step; this should cause the MOOD rule set to
        # fire.
        ted step
        ted querylist {
            SELECT * FROM rule_firings where ruleset='MOOD'
        }
    } -cleanup {
        cleanup
    } -result {
firing_id  5
t          2
driver_id  1001
ruleset    MOOD
rule       MOOD-1-2
fdict      controller JOE delta 100.0 moodThen 0.0 n NB1 dtype MOOD tc 0 moodNow 100.0 g SHIA
    }
    
    test assess-2.2 {Rule 1-2 inputs} -setup {
        setup 100.0
    } -body {
        # Take one more step; this should cause the MOOD rule set to
        # fire.
        ted step
        ted query {
            SELECT rule, g, a, note
            FROM rule_firings JOIN rule_inputs USING (firing_id)
            WHERE ruleset='MOOD'
            ORDER BY g,a
        }
    } -cleanup {
        cleanup
    } -result {
rule     g    a    note        
-------- ---- ---- ----------- 
MOOD-1-2 SHIA BOB  no control  
MOOD-1-2 SHIA DAVE no control  
MOOD-1-2 SHIA JOE  has control 
    }
    
    #-------------------------------------------------------------------
    # sigline

    test sigline-1.1 {sigline output} -setup {
        # None Needed
    } -body {
        driver::MOOD sigline {}
    } -cleanup {
        cleanup
    } -result {Effects of changes to CIV Group MOOD}
         
    
    #-------------------------------------------------------------------
    # narrative

    test narrative-1.1 {narrative output} -body {
        driver::MOOD narrative {
            g       SHIA
            delta   50.0
        } 
    } -cleanup {
        cleanup
    } -result {{group:SHIA}'s mood changed by 50.0}
         
    #-------------------------------------------------------------------
    # detail

    test detail-1.1 {MOOD detail, controller exists} -setup {
        # None needed
    } -body {
        driver::MOOD detail {
            dtype       MOOD
            g           SHIA
            n           N1
            moodNow     30.0
            moodThen    50.0
            delta       20.0
            controller  JOE
            tc          10
        } [namespace current]::ht

        pprint [ht get] 
    } -cleanup {
        cleanup
    } -result {
Civilian group <a href="my://app/group/SHIA">SHIA's</a>
mood has changed by more than 5.0 points
since the last shift in the control of
neighborhood <a href="my://app/nbhood/N1">N1</a>.
SHIA's mood was 50.0,
and is now 30.0.
Neighborhood N1 has been 
controlled by actor <a href="my://app/actor/JOE">JOE</a>
since 2000W11.<p>
    }
    
    test detail-1.2 {MOOD detail, no controller} -setup {
        # None needed
    } -body {
        driver::MOOD detail {
            dtype       MOOD
            g           SHIA
            n           N1
            moodNow     30.0
            moodThen    50.0
            delta       20.0
            controller  ""
            tc          10
        } [namespace current]::ht

        pprint [ht get] 
    } -cleanup {
        cleanup
    } -result {
Civilian group <a href="my://app/group/SHIA">SHIA's</a>
mood has changed by more than 5.0 points
since the last shift in the control of
neighborhood <a href="my://app/nbhood/N1">N1</a>.
SHIA's mood was 50.0,
and is now 30.0.
Neighborhood N1 has been 
without a controller
since 2000W11.<p>
    }
    
    cleanupTests
}

namespace delete ::athena_test::tests::









