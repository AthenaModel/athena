# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#   040-driver_unemp.test
#
# AUTHOR:
#   Will Duquette
#
# DESCRIPTION:
#   UNEMP tests.
#
#   This test suite tests that the rules fire when appropriate.
#
# NOTE:
#   This test suite differs from most of the other 040-* test suites.
#
#   The 040-* tests have the following purposes, in order of importance:
#
#   1. Elimination of lurking bgerrors.
#   2. Verification that the correct rules fire, given the rule set inputs
#   3. Verification that the rules provide the correct kinds of inputs to
#      URAM.
#   4. Verification that the other Athena models will give correct inputs
#      to the rule sets.
#
#   In short, the 040-* test suites test not only the rule sets themselves,
#   but (as noted in point #4) the models that drive the rule sets.
#
#   However, the both the CGE and the cellmodel used to initialize it are
#   in a state of flux; if we use the CGE outputs to drive this test suite,
#   we will be having to modify the tests on a weekly basis as the economic
#   model changes.
#
#   Thus, in this test suite we will skip point #4, and focus narrowly
#   on just the rule sets themselves.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Define an htools buffer for tests
    htools ht

    # pprint text
    #
    # text  - block text
    #
    # Prints it so that it looks nice in a -result option.

    proc pprint {text} {
        return "\n[string trim $text]\n    "
    }

    # setup 
    #
    # Sets up for tests.
    
    proc setup {} {
        # FIRST, other rule sets shouldn't muddy the waters.
        parm set dam.PRESENCE.active no
        parm set dam.ENI.active no
        parm set dam.CONSUMP.active no
        rdb eval {DELETE FROM drivers}
        rdb eval {DELETE FROM mads}
        
        # NEXT, create the entities
        ted create JOE BOB DAVE NB1 NB2 TAL SHIA SUNN KURD
        
        # NEXT, lock, so that URAM is ready to receive inputs.  Disable
        # UNEMP so that it does nothing on lock.
        econ setstate ENABLED
        parm set dam.UNEMP.active no
        ted lock
    }
    
    # unemp uaf
    #
    # uaf  - The unemployment attitude factor for NB1
    #
    # Sets up the demog_n tables with the specified
    # factors, all other factors being 0.0, and calls demsit_rules
    # to assess the results.
    
    proc unemp {uaf} {
        parm set dam.UNEMP.active yes

        # FIRST, spoof the factors.
        rdb eval {
            UPDATE demog_n SET uaf = 0.0;
            UPDATE demog_n SET uaf = $uaf
            WHERE n = 'NB1';
        }
        
        # NEXT, execute the rule set.
        driver::UNEMP assess
    }
    
    # Clean up after a test
    proc cleanup {} {
        ht clear
        ted cleanup
    }

    
    #-------------------------------------------------------------------
    # assess
    
    test assess-1.1 {Rule 1-1 fires} -setup {
        setup
    } -body {
        unemp 1.0
        ted query {
            SELECT rule FROM rule_firings where ruleset='UNEMP'
        }
    } -cleanup {
        cleanup
    } -result {
rule      
--------- 
UNEMP-1-1 
UNEMP-1-1 
    }
    
    test assess-1.2 {Rule 1-1 inputs} -setup {
        setup
    } -body {
        unemp 1.0
        ted query {
            SELECT rule, g, c
            FROM rule_firings JOIN rule_inputs USING (firing_id)
            WHERE ruleset='UNEMP'
        }
    } -cleanup {
        cleanup
    } -result {
rule      g    c   
--------- ---- --- 
UNEMP-1-1 SHIA SFT 
UNEMP-1-1 SHIA AUT 
UNEMP-1-1 SUNN SFT 
UNEMP-1-1 SUNN AUT 
    }

    #-------------------------------------------------------------------
    # sigline

    test sigline-1.1 {sigline output} -setup {
        setup
    } -body {
        driver::UNEMP sigline "NB1 SHIA" 
    } -cleanup {
        cleanup
    } -result {Effect of NB1 unemployment on SHIA}
         

    #-------------------------------------------------------------------
    # narrative

    test narrative-1.1 {narrative output} -setup {
        setup
    } -body {
        driver::UNEMP narrative {
            dtype      UNEMP
            n          NB1
            g          SHIA
            upc        2.4
            uaf        1.0
        } 
    } -cleanup {
        cleanup
    } -result {Unemployment in {nbhood:NB1} affects {group:SHIA}}

         
    #-------------------------------------------------------------------
    # detail

    test detail-1.1 {detail, UNEMP-1} -setup {
        setup
    } -body {
        driver::UNEMP detail {
            dtype      UNEMP
            n          NB1
            g          SHIA
            upc        2.4
            uaf        1.0
        } [namespace current]::ht

        pprint [ht get] 
    } -cleanup {
        cleanup
    } -result {
Unemployment in neighborhood
<a href="my://app/nbhood/NB1">NB1</a>
is at a level of 2.4% unemployed persons per capita;
this affects civilian group
<a href="my://app/group/SHIA">SHIA</a>
with an Unemployment Attitude Factor (UAF) of
1.00.<p>
    }
    

    
    cleanupTests
}

namespace delete ::athena_test::tests::









