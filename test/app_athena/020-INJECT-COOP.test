# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-INJECT-COOP.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    app_sim(1) INJECT:COOP:* order tests.
#
#    This test suite tests the COOP inject's orders.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests
    proc setup {} {
        ted order CURSE:CREATE curse_id C1
        ted order CURSE:CREATE curse_id C2
    }


    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        ted cleanup
    }

    # Standard injects

    set I1 {
        curse_id     C1
        mode         transient
        f            @C1
        g            @F2
        mag          1.0
    }

    set I2 {
        curse_id     C2
        mode         transient
        f            @C1
        g            @F2
        mag          M+ 
    }

    #-------------------------------------------------------------------
    # INJECT:COOP:CREATE

    test CREATE-1.1 {required parms} -body {
        ted order -reject INJECT:COOP:CREATE {}
    } -result {
        curse_id {required value}
        f {required value}
        g {required value}
        mag {required value}
    }

    test CREATE-1.2 {curse_id is unknown} -setup {
        setup
    } -body {
        ted order -reject INJECT:COOP:CREATE {*}$I1 curse_id NONESUCH
    } -cleanup {
        cleanup
    } -result {
        curse_id {Invalid CURSE, should be one of: C1, C2}
    }

    test CREATE-1.3 {mag is invalid} -setup {
        setup
    } -body {
        ted order -reject INJECT:COOP:CREATE {*}$I1 mag NONESUCH
    } -cleanup {
        cleanup
    } -result {
        mag {invalid value "NONESUCH", should be a real number in range -100.0, 100.0, or one of: XXXXL+, XXXL+, XXL+, XL+, L+, M+, S+, XS+, XXS+, XXXS+, ZERO, XXXS-, XXS-, XS-, S-, M-, L-, XL-, XXL-, XXXL-, XXXXL-}
    }

    test CREATE-2.1 {inject is created} -setup {
        setup
    } -body {
        ted order INJECT:COOP:CREATE $I2

        ted querylist {SELECT * FROM gui_injects_COOP}
    } -cleanup {
        cleanup
    } -result {
id          C2 1
curse_id    C2
inject_num  1
mode        transient
longmode    transient
narrative   Change cooperation of civilians in @C1 with forces in @F2 by 5.0 points (M+).
state       normal
f           @C1
g           @F2
mag         5.0
    }

    test CREATE-2.2 {inject is created with existing roles} -setup {
        setup
        ted order INJECT:COOP:CREATE $I1
    } -body {
        ted order INJECT:COOP:CREATE {*}$I2 ftype EXISTING gtype EXISTING

        ted querylist {SELECT * FROM gui_injects_COOP}
    } -cleanup {
        cleanup
    } -result {
id          C1 1
curse_id    C1
inject_num  1
mode        transient
longmode    transient
narrative   Change cooperation of civilians in @C1 with forces in @F2 by 1.0 points (XXXS+).
state       normal
f           @C1
g           @F2
mag         1.0

id          C2 1
curse_id    C2
inject_num  1
mode        transient
longmode    transient
narrative   Change cooperation of civilians in @C1 with forces in @F2 by 5.0 points (M+).
state       normal
f           @C1
g           @F2
mag         5.0
    }

    test CREATE-2.3 {Effects} -setup {
        setup
    } -body {
        ted notifier bind ::rdb <curse_injects>
        ted order INJECT:COOP:CREATE $I1
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <curse_injects> update {C1 1}}
    }


    test CREATE-3.1 {undo effects} -setup {
        setup
        ted order INJECT:COOP:CREATE $I1
    } -body {
        ted notifier bind ::rdb <curse_injects>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <curse_injects> delete {C1 1}}
    }

    #-------------------------------------------------------------------
    # INJECT:COOP:UPDATE

    proc setup_update_coop {} {
        variable I2
        setup
        ted order INJECT:COOP:CREATE $I2
    }

    test UPDATE-1.1 {required parms} -body {
        ted order -reject INJECT:COOP:UPDATE {}
    } -result {
        id {required value}
    }
    
    test UPDATE-1.2 {CURSE in id is unknown} -setup {
        setup_update_coop
    } -body {
        ted order -reject INJECT:COOP:UPDATE id {NONESUCH 1}
    } -cleanup {
        cleanup
    } -result {
        id {Invalid CURSE, should be one of: C1, C2}
    }

    test UPDATE-1.3 {inject number in id is unknown} -setup {
        setup_update_coop
    } -body {
        ted order -reject INJECT:COOP:UPDATE id {C1 NONESUCH}
    } -cleanup {
        cleanup
    } -result {
        id {Invalid inject "C1 NONESUCH", no injects are defined for this CURSE}
    }

    test UPDATE-1.4 {mag is invalid} -setup {
        setup_update_coop
    } -body {
        ted order -reject INJECT:COOP:UPDATE id {C2 1} mag NONESUCH
    } -cleanup {
        cleanup
    } -result {
        mag {invalid value "NONESUCH", should be a real number in range -100.0, 100.0, or one of: XXXXL+, XXXL+, XXL+, XL+, L+, M+, S+, XS+, XXS+, XXXS+, ZERO, XXXS-, XXS-, XS-, S-, M-, L-, XL-, XXL-, XXXL-, XXXXL-}
    }

    test UPDATE-2.1 {inject is updated} -setup {
        setup_update_coop
    } -body {
        ted order INJECT:COOP:UPDATE id {C2 1} f @C9 g @F9 mag 10.0

        ted querylist {SELECT * FROM gui_injects_COOP}
    } -cleanup {
        cleanup
    } -result {
id          C2 1
curse_id    C2
inject_num  1
mode        transient
longmode    transient
narrative   Change cooperation of civilians in @C9 with forces in @F9 by 10.0 points (XL+).
state       normal
f           @C9
g           @F9
mag         10.0
    }

    test UPDATE-2.2 {Effects} -setup {
        setup
        ted order INJECT:COOP:CREATE $I2
    } -body {
        ted notifier bind ::rdb <curse_injects>
        ted order INJECT:COOP:UPDATE id {C2 1} g BLUE
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <curse_injects> update {C2 1}}
        {::rdb <curse_injects> update {C2 1}}
    }


    test UPDATE-3.1 {undo effects} -setup {
        setup_update_coop
        ted order INJECT:COOP:UPDATE id {C2 1} g BLUE
    } -body {
        ted notifier bind ::rdb <curse_injects>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <curse_injects> update {C2 1}}
    }


    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





