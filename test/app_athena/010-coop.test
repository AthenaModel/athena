# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-coop.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    coop(sim) tests.
#
#    This test suite tests the coop(sim) mutators and queries
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        ted cleanup
    }

    #-------------------------------------------------------------------
    # mutate update


    test update-1.1 {coop is updated} -setup {
        ted create NB1 BLUE SHIA
    } -body {
        coop update {
            id         {SHIA BLUE}
            base       40.0
            regress_to BASELINE
            natural    {}
        }

        ted query {SELECT * FROM coop_fg}
    } -cleanup {
        cleanup
    } -result {
f    g    base regress_to natural 
---- ---- ---- ---------- ------- 
SHIA BLUE 40.0 BASELINE   50.0    
    }


    test update-1.2 {coop is updated} -setup {
        ted create NB1 BLUE SHIA
    } -body {
        coop update {
            id         {SHIA BLUE}
            base       40.0
            regress_to NATURAL
            natural    70.0
        }

        ted query {SELECT * FROM coop_fg}
    } -cleanup {
        cleanup
    } -result {
f    g    base regress_to natural 
---- ---- ---- ---------- ------- 
SHIA BLUE 40.0 NATURAL    70.0    
    }

    test update-1.3 {empty values are OK} -setup {
        ted create NB1 BLUE SHIA
    } -body {
        set a [ted query {SELECT * FROM coop_fg}]

        coop update  {
            id         {SHIA BLUE}
            base       ""
            regress_to ""
            natural    ""
        }

        set b [ted query {SELECT * FROM coop_fg}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    test update-2.1 {undo undoes the change} -setup {
        ted create NB1 BLUE SHIA
    } -body {

        set a [ted query {SELECT * FROM coop_fg}]

        set undo [coop update {
            id         {SHIA BLUE}
            base       40.0
            regress_to BASELINE
            natural    {}
        }]

        eval $undo

        set b [ted query {SELECT * FROM coop_fg}]

        expr {$a eq $b}
    } -cleanup {
        cleanup
    } -result {1}


    #-------------------------------------------------------------------
    # exists

    test exists-1.1 {coop does not exist} -body {
        coop exists NONESUCH NONESUCH
    } -cleanup {
        cleanup
    } -result {0}


    test exists-1.2 {coop exists} -setup {
        ted create NB1 BLUE SHIA
    } -body {
        coop exists SHIA BLUE
    } -cleanup {
        cleanup
    } -result {1}


    #-------------------------------------------------------------------
    # validate

    test validate-1.1 {invalid group f} -setup {
        ted create NB1 BLUE SHIA
    } -body {
        coop validate {NONESUCH NONESUCH}
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid civilian group, should be one of: SHIA}


    test validate-1.2 {Invalid group g} -setup {
        ted create NB1 BLUE SHIA
    } -body {
        coop validate {SHIA NONESUCH}
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid force group, should be one of: BLUE}


    test validate-2.1 {Valid group} -setup {
        ted create NB1 BLUE SHIA
    } -body {
        coop validate {SHIA BLUE}
    } -cleanup {
        cleanup
    } -result {SHIA BLUE}



    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





