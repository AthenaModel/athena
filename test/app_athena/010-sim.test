# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-sim.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    sim(sim) tests.
#
#    This test suite tests the sim(sim) mutators and queries
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Get initial time 0
    set clockdata [simclock checkpoint]
    
    # Clean up after a test

    proc cleanup {} {
        variable clockdata

        # Clean up the test environment
        simclock restore $clockdata

        if {[sim state] eq "RUNNING"} {
            sim mutate pause
        }

        if {[sim state] eq "PAUSED"} {
            sim restart
        }

        ted cleanup

        assert {![aram initialized]}
    }

    #-------------------------------------------------------------------
    # init
    #
    # Verify initial module state
   

    test init-1.1 {Initial state} -body {
        list \
            [sim now]         \
            [sim state]       \
            [sim stoptime]    \
            [simclock asString]
        
    } -result {0 PREP 0 2012W01}

    #-------------------------------------------------------------------
    # mutate startdate

    test mutate_startdate-1.1 {Sets the simclock's start date} -body {
        sim mutate startdate 2014W26
        simclock cget -week0
    } -cleanup {
        cleanup
    } -result {2014W26}

    
    test mutate_startdate-1.2 {Sends <Time>} -body {
        ted notifier bind ::sim <Time>
        sim mutate startdate 2014W26
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::sim <Time>}
    }


    test mutate_startdate-1.3 {Returns undo script} -body {
        sim mutate startdate 2014W26
    } -cleanup {
        cleanup
    } -result {::sim mutate startdate 2012W01}


    test mutate_startdate-1.4 {Sets "changed"} -body {
        sim mutate startdate 2014W26
        sim changed
    } -cleanup {
        cleanup
    } -result {1}

    #-------------------------------------------------------------------
    # mutate starttick

    test mutate_starttick-1.1 {Sets the simclock's start tick} -body {
        sim mutate starttick 52
        simclock cget -tick0
    } -cleanup {
        cleanup
    } -result {52}

    
    test mutate_starttick-1.2 {Sends <Time>} -body {
        ted notifier bind ::sim <Time>
        sim mutate starttick 52
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::sim <Time>}
    }


    test mutate_starttick-1.3 {Returns undo script} -body {
        sim mutate starttick 52
    } -cleanup {
        cleanup
    } -result {::sim mutate starttick 0}


    test mutate_starttick-1.4 {Sets "changed"} -body {
        sim mutate starttick 52
        sim changed
    } -cleanup {
        cleanup
    } -result {1}

    #-------------------------------------------------------------------
    # mutate lock

    test mutate_lock-1.1 {lock sets state to PAUSED} -setup {
        ted create NB1 BLUE SHIA
        parm set econ.gdpExp 0
        parm set econ.empExp 0
    } -body {
        sim mutate lock

        sim state
    } -cleanup {
        cleanup
    } -result {PAUSED}


    test mutate_lock-1.2 {must be in PREP} -setup {
        ted create NB1 BLUE SHIA
        parm set econ.gdpExp 0
        parm set econ.empExp 0
        sim mutate lock
    } -body {
        sim mutate lock
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Assertion failed: $info(state) eq "PREP"}

    test mutate_lock-1.3 {Saves a PREP snapshot} -setup {
        ted create NB1 BLUE SHIA
        parm set econ.gdpExp 0
        parm set econ.empExp 0
    } -body {
        sim mutate lock

        rdb eval {SELECT tick FROM snapshots}
    } -cleanup {
        cleanup
    } -result {-1}

    test mutate_lock-1.4 {Initializes URAM} -setup {
        ted create NB1 BLUE SHIA
        parm set econ.gdpExp 0
        parm set econ.empExp 0
    } -body {
        sim mutate lock

        rdb onecolumn {SELECT n FROM uram_n}
    } -cleanup {
        cleanup
    } -result {NB1}

    #-------------------------------------------------------------------
    # mutate run/mutate pause


    test mutate_runpause-1.1 {No stop time} -setup {
        ted create NB1 BLUE SHIA
        parm set econ.gdpExp 0
        parm set econ.empExp 0
    } -body {
        sim mutate lock
        sim mutate run
        sim stoptime
    } -cleanup {
        cleanup
    } -result {0}

    
    test mutate_runpause-1.2 {Run for -ticks} -setup {
        ted create NB1 BLUE SHIA
        parm set econ.gdpExp 0
        parm set econ.empExp 0
    } -body {
        sim mutate lock
        sim mutate run -ticks 5
        sim stoptime
    } -cleanup {
        cleanup
    } -result {5}

    test mutate_runpause-1.3 {Run -until} -setup {
        ted create NB1 BLUE SHIA
        parm set econ.gdpExp 0
        parm set econ.empExp 0
    } -body {
        sim mutate lock
        sim mutate run -until 10
        sim stoptime
    } -cleanup {
        cleanup
    } -result {10}

    test mutate_runpause-1.4 {Sets state to "RUNNING"} -setup {
        ted create NB1 BLUE SHIA
        parm set econ.gdpExp 0
        parm set econ.empExp 0
    } -body {
        sim mutate lock
        sim mutate run
        sim state
    } -cleanup {
        cleanup
    } -result {RUNNING}


    test mutate_runpause-1.5 {Can't run unless "PAUSED"} -setup {
        ted create NB1 BLUE SHIA
        parm set econ.gdpExp 0
        parm set econ.empExp 0
    } -body {
        sim mutate lock
        sim mutate run
        sim mutate run
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Assertion failed: $info(state) eq "PAUSED"}


    test mutate_runpause-1.6 {Sends <State>} -setup {
        ted create NB1 BLUE SHIA
        parm set econ.gdpExp 0
        parm set econ.empExp 0
    } -body {
        sim mutate lock
        ted notifier bind ::sim <State>
        sim mutate run
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::sim <State>}
    }


    test mutate_runpause-1.7 {Returns undo script} -setup {
        ted create NB1 BLUE SHIA
        parm set econ.gdpExp 0
        parm set econ.empExp 0
    } -body {
        sim mutate lock
        sim mutate run
    } -cleanup {
        cleanup
    } -result {}


    test mutate_runpause-2.1 {Pauses if running} -setup {
        ted create NB1 BLUE SHIA
        parm set econ.gdpExp 0
        parm set econ.empExp 0
    } -body {
        sim mutate lock
        sim mutate run
        simclock tick
        sim mutate pause
        sim state
    } -cleanup {
        cleanup
    } -result {PAUSED}


    test mutate_runpause-2.2 {Sends <State>} -setup {
        ted create NB1 BLUE SHIA
        parm set econ.gdpExp 0
        parm set econ.empExp 0
    } -body {
        sim mutate lock
        sim mutate run
        ted notifier bind ::sim <State>
        sim mutate pause
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::sim <State>}
    }

    
    test mutate_runpause-2.3 {Returns undo script} -setup {
        ted create NB1 BLUE SHIA
    } -body {
        sim mutate pause
    } -cleanup {
        cleanup
    } -result {}


    test mutate_runpause-2.4 {Runs for some number of ticks} -setup {
        ted create NB1 BLUE SHIA
        parm set econ.gdpExp 0
        parm set econ.empExp 0
    } -body {
        sim mutate lock
        sim mutate run -ticks 5 -block yes
        list [simclock now] [sim state]
    } -cleanup {
        cleanup
    } -result {5 PAUSED}

    #-------------------------------------------------------------------
    # mutate unlock

    test mutate_unlock-1.1 {Can't unlock in PREP} -body {
        sim mutate unlock
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Assertion failed: $info(state) eq "PAUSED"}

    test mutate_unlock-1.2 {Sets state to PREP} -setup {
        ted create NB1 BLUE SHIA
        parm set econ.gdpExp 0
        parm set econ.empExp 0
        sim mutate lock
    } -body {
        sim mutate unlock
        sim state
    } -cleanup {
        cleanup
    } -result {PREP}

    test mutate_unlock-1.3 {Purges snapshot} -setup {
        ted create NB1 BLUE SHIA
        parm set econ.gdpExp 0
        parm set econ.empExp 0
        sim mutate lock
        sim mutate run -ticks 1 -block yes
    } -body {
        sim mutate unlock
        rdb eval {SELECT tick FROM snapshots}
    } -cleanup {
        cleanup
    } -result {}

    #-------------------------------------------------------------------
    # now

    test now-1.1 {Returns 0 at time 0} -body {
        expr {[sim now] == [simclock now]}
    } -result {1}


    #-------------------------------------------------------------------
    # state

    test state-1.1 {Returns current state} -setup {
        ted create NB1 BLUE SHIA
        parm set econ.gdpExp 0
        parm set econ.empExp 0
    } -body {
        set states [list]
        
        lappend states [sim state]
        sim mutate lock
        lappend states [sim state]
        sim mutate run
        simclock tick
        lappend states [sim state]
        sim mutate pause
        lappend states [sim state]
        sim restart
        lappend states [sim state]

        set states
    } -cleanup {
        cleanup
    } -result {PREP PAUSED RUNNING PAUSED PREP}


    #-------------------------------------------------------------------
    # stoptime

    test stoptime-1.1 {Returns current stoptime} -setup {
        ted create NB1 BLUE SHIA
        parm set econ.gdpExp 0
        parm set econ.empExp 0
    } -body {
        set times [list]

        lappend times [sim stoptime]

        sim mutate lock
        sim mutate run
        lappend times [sim stoptime]
        sim mutate pause
        
        sim mutate run -ticks 5
        lappend times [sim stoptime]
        sim mutate pause

        sim mutate run -until 10
        lappend times [sim stoptime]

        set times
    } -cleanup {
        cleanup
    } -result {0 0 6 10}



    #-------------------------------------------------------------------
    # restart
    #
    # NOTE: sim restart is tested pretty thoroughly during the "cleanup"
    # process and by the other tests.

    test restart-1.1 {Check results} -setup {
        ted create NB1 BLUE SHIA
        parm set econ.gdpExp 0
        parm set econ.empExp 0
        sim mutate lock
        sim mutate run
        simclock advance 5
        sim mutate pause
        sim mutate startdate 2014W26
        # TBD: Should schedule something in the event queue
    } -body {
        sim restart

        list \
            [sim state]         \
            [sim now]           \
            [sim changed]       \
            [simclock cget -week0]
    } -cleanup {
        cleanup
    } -result {PREP 0 1 2012W01}
    
    #-------------------------------------------------------------------
    # rebase
    #
    # Note: this tests [sim mutate rebase] specifically; more general
    # rebase testing is done in 010-rebase.test.
    
    test rebase-1.1 {in prep after rebase} -setup {
        ted create NB1 BLUE SHIA
        ted lock
        ted step
        ted step
    } -body {
        sim mutate rebase
        list \
            [sim state] \
            [simclock now] \
            [simclock cget -tick0]
    } -cleanup {
        cleanup
    } -result {PREP 2 2}
        

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::






