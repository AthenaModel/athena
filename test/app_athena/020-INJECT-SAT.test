# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-INJECT-SAT.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    app_sim(1) INJECT:SAT:* order tests.
#
#    This test suite tests the SAT inject's orders.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests
    proc setup {} {
        ted order CURSE:CREATE curse_id C1
        ted order CURSE:CREATE curse_id C2
    }


    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        ted cleanup
    }

    # Standard injects

    set I1 {
        curse_id     C1
        mode         transient
        g            @C1
        c            SFT
        mag          1.0
    }

    set I2 {
        curse_id     C2
        mode         transient
        g            @C1
        c            SFT
        mag          M+ 
    }

    #-------------------------------------------------------------------
    # INJECT:SAT:CREATE

    test CREATE-1.1 {required parms} -body {
        ted orderx -reject INJECT:SAT:CREATE {}
    } -result {
        curse_id {required value}
        g {required value}
        c {required value}
        mag {required value}
    }

    test CREATE-1.2 {curse_id is unknown} -setup {
        setup
    } -body {
        ted orderx -reject INJECT:SAT:CREATE {*}$I1 curse_id NONESUCH
    } -cleanup {
        cleanup
    } -result {
        curse_id {Invalid CURSE, should be one of: C1, C2}
    }

    test CREATE-1.3 {mag is invalid} -setup {
        setup
    } -body {
        ted orderx -reject INJECT:SAT:CREATE {*}$I1 mag NONESUCH
    } -cleanup {
        cleanup
    } -result {
        mag {invalid value "NONESUCH", should be a real number in range -100.0, 100.0, or one of: XXXXL+, XXXL+, XXL+, XL+, L+, M+, S+, XS+, XXS+, XXXS+, ZERO, XXXS-, XXS-, XS-, S-, M-, L-, XL-, XXL-, XXXL-, XXXXL-}
    }

    test CREATE-2.1 {inject is created} -setup {
        setup
    } -body {
        ted orderx INJECT:SAT:CREATE $I2

        ted querylist {SELECT * FROM gui_injects_SAT}
    } -cleanup {
        cleanup
    } -result {
id          C2 1
curse_id    C2
inject_num  1
mode        transient
longmode    transient
narrative   Change satisfaction of civilians in @C1 with SFT by 5.0 points (M+).
state       normal
g           @C1
c           SFT
mag         5.0
    }

    test CREATE-2.2 {Effects} -setup {
        setup
    } -body {
        ted notifier bind ::rdb <curse_injects>
        ted orderx INJECT:SAT:CREATE $I1
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <curse_injects> update {C1 1}}
    }


    test CREATE-3.1 {undo effects} -setup {
        setup
        ted orderx INJECT:SAT:CREATE $I1
    } -body {
        ted notifier bind ::rdb <curse_injects>
        flunky undo
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <curse_injects> delete {C1 1}}
    }

    #-------------------------------------------------------------------
    # INJECT:SAT:UPDATE

    proc setup_update_sat {} {
        variable I2
        setup
        ted orderx INJECT:SAT:CREATE $I2
    }

    test UPDATE-1.1 {required parms} -body {
        ted orderx -reject INJECT:SAT:UPDATE {}
    } -result {
        id {required value}
    }
    
    test UPDATE-1.2 {CURSE in id is unknown} -setup {
        setup_update_sat
    } -body {
        ted orderx -reject INJECT:SAT:UPDATE id {NONESUCH 1}
    } -cleanup {
        cleanup
    } -result {
        id {Invalid CURSE, should be one of: C1, C2}
    }

    test UPDATE-1.3 {inject number in id is unknown} -setup {
        setup_update_sat
    } -body {
        ted orderx -reject INJECT:SAT:UPDATE id {C1 NONESUCH}
    } -cleanup {
        cleanup
    } -result {
        id {Invalid inject "C1 NONESUCH", no injects are defined for this CURSE}
    }

    test UPDATE-1.4 {mag is invalid} -setup {
        setup_update_sat
    } -body {
        ted orderx -reject INJECT:SAT:UPDATE id {C2 1} mag NONESUCH
    } -cleanup {
        cleanup
    } -result {
        mag {invalid value "NONESUCH", should be a real number in range -100.0, 100.0, or one of: XXXXL+, XXXL+, XXL+, XL+, L+, M+, S+, XS+, XXS+, XXXS+, ZERO, XXXS-, XXS-, XS-, S-, M-, L-, XL-, XXL-, XXXL-, XXXXL-}
    }

    test UPDATE-2.1 {inject is updated} -setup {
        setup_update_sat
    } -body {
        ted orderx INJECT:SAT:UPDATE id {C2 1} g @C9 c AUT mag 10.0

        ted querylist {SELECT * FROM gui_injects_SAT}
    } -cleanup {
        cleanup
    } -result {
id          C2 1
curse_id    C2
inject_num  1
mode        transient
longmode    transient
narrative   Change satisfaction of civilians in @C9 with AUT by 10.0 points (XL+).
state       normal
g           @C9
c           AUT
mag         10.0
    }

    test UPDATE-2.2 {Effects} -setup {
        setup
        ted orderx INJECT:SAT:CREATE $I2
    } -body {
        ted notifier bind ::rdb <curse_injects>
        ted orderx INJECT:SAT:UPDATE id {C2 1} g BLUE
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <curse_injects> update {C2 1}}
        {::rdb <curse_injects> update {C2 1}}
    }


    test UPDATE-3.1 {undo effects} -setup {
        setup_update_sat
        ted orderx INJECT:SAT:UPDATE id {C2 1} g BLUE
    } -body {
        ted notifier bind ::rdb <curse_injects>
        flunky undo
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::rdb <curse_injects> update {C2 1}}
    }


    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





