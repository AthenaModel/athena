# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-tactic_spend.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    tactic_spend(sim) tests.
#
#    This test suite tests the tactic_spend(sim) class.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    proc setup {} {
        variable T1
        variable coffer
        ted create JOE BOB NB1 NB2 BLUE BRIT SHIA SUNN KURD
        cash load
        set coffer [coffer new JOE]


        set B1 [ted addblock JOE]
        set T1 [ted addtactic $B1 SPEND]
    }

    # Clean up after a test

    proc cleanup {} {
        variable coffer
        $coffer destroy
        ted cleanup
    }

    #-------------------------------------------------------------------
    # Constructor

    test constructor-1.1 {default state} -setup {
        setup
    } -body {
        ted pdicts [$T1 getdict]
    } -cleanup {
        cleanup
    } -match dictglob -result {
        amount     0.0
        black      1
        execstatus NONE
        faildict   {}
        goods      1
        id         *
        mode       ALL
        parent     *
        percent    0.0
        pop        1
        pot        ::pot
        region     1
        state      normal
        world      1
    }

    test constructor-1.2 {can set options} -setup {
        setup
    } -body {
        set T1 [tactic::SPEND new -parent DUMMY]
        $T1 get parent
    } -cleanup {
        cleanup
    } -result {DUMMY}
    

    #-------------------------------------------------------------------
    # narrative

    test narrative-1.1 {narrative, default state} -setup {
        setup
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Spend all remaining cash-on-hand according to the following profile: goods: 20%; black: 20%; pop: 20%; region: 20%; world: 20%}

    test narrative-1.2 {narrative, with exact amount} -setup {
        setup
        $T1 set mode EXACT
        $T1 set amount 10.0
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Spend exactly $10.00 of cash-on-hand according to the following profile: goods: 20%; black: 20%; pop: 20%; region: 20%; world: 20%}

    test narrative-1.3 {narrative, with up to amount} -setup {
        setup
        $T1 set mode UPTO
        $T1 set amount 10.0
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Spend up to $10.00 of cash-on-hand according to the following profile: goods: 20%; black: 20%; pop: 20%; region: 20%; world: 20%}

    test narrative-1.4 {narrative, with percentage} -setup {
        setup
        $T1 set mode PERCENT
        $T1 set amount 10.0
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Spend 0.0% of cash-on-hand according to the following profile: goods: 20%; black: 20%; pop: 20%; region: 20%; world: 20%}

    test narrative-1.5 {narrative, with excess amount} -setup {
        setup
        $T1 set mode EXCESS
        $T1 set amount 10.0
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Spend any cash-on-hand over $10.00 according to the following profile: goods: 20%; black: 20%; pop: 20%; region: 20%; world: 20%}

    #-------------------------------------------------------------------
    # check

    test check-1.1 {Initial state succeeds} -setup {
        setup
    } -body {
        $T1 check
    } -cleanup {
        cleanup
    } -result {}

    #-------------------------------------------------------------------
    # obligate

    test obligate-1.1 {ALL cash} -setup {
        setup
    } -body {
        $T1 set mode ALL

        set a [$T1 obligate $coffer]
        set b [$coffer cash]

        list $a $b
    } -cleanup {
        cleanup
    } -result {1 0.0}

    test obligate-1.2 {Sufficient cash} -setup {
        setup
    } -body {
        $T1 set mode EXACT
        $T1 set amount 1000.0

        set a [$T1 obligate $coffer]
        set b [$coffer cash]

        list $a $b
    } -cleanup {
        cleanup
    } -result {1 9000.0}

    test obligate-1.3 {Insufficient cash} -setup {
        setup
    } -body {
        $T1 set mode EXACT
        $T1 set amount 10001.0

        $T1 obligate $coffer
        $T1 faildict
    } -cleanup {
        cleanup
    } -result {CASH {Required $10,001, but had only $10,000.}}

    test obligate-1.4 {Insufficient cash on lock} -setup {
        setup
        strategy locking 1
    } -body {
        $T1 set mode EXACT
        $T1 set amount 10001.0

        set a [$T1 obligate $coffer]
        set b [$coffer cash]

        list $a $b
    } -cleanup {
        cleanup
    } -result {1 0.0}

    test obligate-1.5 {No cash on lock} -setup {
        setup
        strategy locking 1
    } -body {
        $T1 set mode EXACT
        $T1 set amount 10001.0
        $coffer spend [$coffer cash]
        
        set a [$T1 obligate $coffer]
        set b [$coffer cash]

        list $a $b
    } -cleanup {
        cleanup
    } -result {1 0.0}

    test obligate-1.6 {Up to cash (> on hand)} -setup {
        setup
    } -body {
        $T1 set mode UPTO
        $T1 set amount 10001.0

        set a [$T1 obligate $coffer]
        set b [$coffer cash]

        list $a $b
    } -cleanup {
        cleanup
    } -result {1 0.0}

    test obligate-1.7 {Up to cash (< on hand)} -setup {
        setup
    } -body {
        $T1 set mode UPTO
        $T1 set amount 1000.0

        set a [$T1 obligate $coffer]
        set b [$coffer cash]

        list $a $b
    } -cleanup {
        cleanup
    } -result {1 9000.0}

    test obligate-1.8 {Excess of cash on hand} -setup {
        setup
    } -body {
        $T1 set mode EXCESS
        $T1 set amount 1000.0

        set a [$T1 obligate $coffer]
        set b [$coffer cash]

        list $a $b
    } -cleanup {
        cleanup
    } -result {1 1000.0}

    test obligate-1.9 {Percentage of cash on hand} -setup {
        setup
    } -body {
        $T1 set mode PERCENT
        $T1 set percent 10.0

        set a [$T1 obligate $coffer]
        set b [$coffer cash]

        list $a $b
    } -cleanup {
        cleanup
    } -result {1 9000.0}


    #-------------------------------------------------------------------
    # execute

    test execute-1.1 {always succeeds} -setup {
        setup
        $T1 set mode EXACT
        $T1 set amount 1000.0
        $T1 obligate $coffer
    } -body {
        $T1 execute
        rdb onecolumn {SELECT narrative FROM sigevents WHERE component='tactic'}
    } -cleanup {
        cleanup
    } -result {SPEND: Actor {actor:JOE} spends 1000.0 on goods: 20%; black: 20%; pop: 20%; region: 20%; world: 20%}

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::









