# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-SITUATION-ENVIRONMENTAL.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    app_sim(1) ABSIT:* order tests.
#
#    This test suite tests the absit-related orders.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Standard units
    set s1 {
        n         NB1
        stype     BADWATER
        coverage  1.0
        inception 1
        resolver  NONE
    }

    set s2 {
        n         NB1
        stype     COMMOUT
        coverage  0.5
        inception 0
        resolver  BLUE
    }

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        ted cleanup
    }

    #-------------------------------------------------------------------
    # ABSIT:CREATE

    test CREATE-1.1 {required parms} -body {
        ted order -reject ABSIT:CREATE {}
    } -result {
        n {required value}
        stype {required value}
    }

    
    test CREATE-1.2 {invalid n} -body {
        ted create BLUE
        ted order -reject ABSIT:CREATE \
            {*}$s1 n NONESUCH
    } -cleanup {
        cleanup
    } -result {
        n {Invalid neighborhood, none are defined}
    }


    test CREATE-1.5 {invalid stype} -body {
        ted create BLUE NB1
        ted order -reject ABSIT:CREATE \
            {*}$s1 stype NONESUCH
    } -cleanup {
        cleanup
    } -match glob -result {
        stype {invalid value "NONESUCH", should be one of: BADFOOD, *}
    }


    test CREATE-1.6 {duplicate stype} -setup {
        ted create BLUE NB1
        ted create NB1
        ted order ABSIT:CREATE $s1
    } -body {
        ted order -reject ABSIT:CREATE $s1
    } -cleanup {
        cleanup
    } -result {
        stype {An absit of this type already exists in this neighborhood.}
    }


    test CREATE-1.7 {invalid coverage} -body {
        ted create BLUE NB1
        ted order -reject ABSIT:CREATE \
            {*}$s1 coverage 2.0
    } -cleanup {
        cleanup
    } -result {
        coverage {invalid value "2.0", should be a real number in range 0.0, 1.0}
    }


    test CREATE-1.8 {coverage can't be 0.0} -setup {
        ted create NB1 BLUE
    } -body {
        ted order -reject ABSIT:CREATE \
            {*}$s1 coverage 0.0
    } -cleanup {
        cleanup
    } -result {
        coverage {Coverage must be greater than 0.}
    }


    test CREATE-1.9 {invalid inception} -body {
        ted create BLUE NB1
        ted order -reject ABSIT:CREATE \
            {*}$s1 inception NONESUCH
    } -cleanup {
        cleanup
    } -result {
        inception {invalid boolean "NONESUCH", should be one of: 1, 0, true, false, yes, no, on, off}
    }


    test CREATE-1.10 {invalid resolver} -body {
        ted create BLUE NB1
        ted order -reject ABSIT:CREATE \
            {*}$s1 resolver NONESUCH
    } -cleanup {
        cleanup
    } -result {
        resolver {Invalid group "NONESUCH", should be one of: NONE, BLUE}
    }


    test CREATE-1.11 {invalid rduration} -body {
        ted create BLUE NB1
        ted order -reject ABSIT:CREATE \
            {*}$s1 rduration NONESUCH
    } -cleanup {
        cleanup
    } -result {
        rduration {invalid value "NONESUCH", expected integer}
    }



    test CREATE-2.1 {absit is created} -setup {
        ted create NB1 BLUE
    } -body {
        ted order ABSIT:CREATE $s1

        ted querylist {SELECT * FROM absits}
    } -cleanup {
        cleanup
    } -match glob -result {
s          1
stype      BADWATER
n          NB1
coverage   1.0
inception  1
resolver   NONE
rduration  1
state      INITIAL
ts         0
tr         1
location   *
    }


    test CREATE-2.2 {Effects} -setup {
        ted create NB1 BLUE
    } -body {
        ted notifier bind ::adb <absits>
        ted order ABSIT:CREATE $s1
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::adb <absits> update 1}
    }


    test CREATE-3.1 {undo effects} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
    } -body {
        ted notifier bind ::adb <absits>
        adb order undo
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::adb <absits> delete 1}
    }


    #-------------------------------------------------------------------
    # ABSIT:DELETE

    test DELETE-1.1 {required parms} -body {
        ted order -reject ABSIT:DELETE {}
    } -result {
        s {required value}
    }

    
    test DELETE-1.2 {unknown s} -body {
        ted order -reject ABSIT:DELETE s NONESUCH
    } -result {
        s {not a "live" situation: "NONESUCH"}
    }


    test DELETE-1.3 {s not in initial state} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
        rdb eval {UPDATE absits SET state='ONGOING' WHERE s=1}
    } -body {
        ted order -reject ABSIT:DELETE s 1
    } -cleanup {
        cleanup
    } -result {
        s {operation is invalid; time has passed.}
    }


    test DELETE-2.1 {Effects} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
    } -body {
        ted notifier bind ::adb <absits>
        ted order ABSIT:DELETE s 1
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::adb <absits> delete 1}
    }


    test DELETE-3.1 {Undo effects} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
        ted order ABSIT:DELETE s 1
    } -body {
        ted notifier bind ::adb <absits>
        adb order undo
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::adb <absits> update 1}
    }


    #-------------------------------------------------------------------
    # ABSIT:UPDATE

    # 1.*: parameter type validation
    # 2.*: correct operation
    # 3.*: undo

    test UPDATE-1.1 {required parms} -body {
        ted order -reject ABSIT:UPDATE {}
    } -result {
        s {required value}
    }
    
    test UPDATE-1.2 {invalid n} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
    } -body {
        ted order -reject ABSIT:UPDATE s 1 n NONESUCH
    } -cleanup {
        cleanup
    } -result {
        n {Invalid neighborhood, should be one of: NB1}
    }


    test UPDATE-1.3 {invalid stype} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
    } -body {
        ted order -reject ABSIT:UPDATE s 1 stype NONESUCH
    } -cleanup {
        cleanup
    } -match glob -result {
        stype {invalid value "NONESUCH", should be one of: BADFOOD, *}
    }


    test UPDATE-1.4 {invalid coverage} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
    } -body {
        ted order -reject ABSIT:UPDATE s 1 coverage NONESUCH
    } -cleanup {
        cleanup
    } -result {
        coverage {invalid value "NONESUCH", should be a real number in range 0.0, 1.0}
    }


    test UPDATE-1.5 {invalid inception} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
    } -body {
        ted order -reject ABSIT:UPDATE s 1 inception NONESUCH
    } -cleanup {
        cleanup
    } -result {
        inception {invalid boolean "NONESUCH", should be one of: 1, 0, true, false, yes, no, on, off}
    }


    test UPDATE-1.6 {invalid resolver} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
    } -body {
        ted order -reject ABSIT:UPDATE s 1 resolver NONESUCH
    } -cleanup {
        cleanup
    } -result {
        resolver {Invalid group "NONESUCH", should be one of: NONE, BLUE}
    }


    test UPDATE-1.7 {invalid rduration} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
    } -body {
        ted order -reject ABSIT:UPDATE s 1 rduration NONESUCH
    } -cleanup {
        cleanup
    } -result {
        rduration {invalid value "NONESUCH", expected integer}
    }

    test UPDATE-1.8 {Not INITIAL state} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
        rdb eval {UPDATE absits SET state='ONGOING' WHERE s=1}
    } -body {
        ted order -reject ABSIT:UPDATE s 1 \
            stype     COMMOUT \
            coverage  0.5     \
            inception NO      \
            resolver  NONE
    } -cleanup {
        cleanup
    } -result {
        s {operation is invalid; time has passed.}
    }


    test UPDATE-2.1 {absit is updated} -setup {
        ted create NB1 NB2 BLUE
        ted order ABSIT:CREATE $s1
    } -body {
        ted order ABSIT:UPDATE {
            s         1 
            n         NB2
            stype     COMMOUT
            inception NO
            resolver  BLUE
            rduration 10
        }

        ted querylist {SELECT * FROM absits}
    } -cleanup {
        cleanup
    } -match glob -result {
s          1
stype      COMMOUT
n          NB2
coverage   1.0
inception  0
resolver   BLUE
rduration  10
state      INITIAL
ts         0
tr         10
location   *
    }


    test UPDATE-2.2 {table monitor event} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
    } -body {
        ted notifier bind ::adb <absits>
        ted order ABSIT:UPDATE s 1 stype COMMOUT
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::adb <absits> update 1}
    }


    test UPDATE-3.1 {undo undoes the change} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
        ted order ABSIT:UPDATE {
            s         1 
            stype     COMMOUT
            inception NO
        }
    } -body {
        ted notifier bind ::adb <absits>
        adb order undo
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::adb <absits> update 1}
    }


    #-------------------------------------------------------------------
    # ABSIT:MOVE

    # 1.*: parameter type validation
    # 2.*: correct operation
    # 3.*: undo

    test MOVE-1.1 {required parms} -body {
        ted order -reject ABSIT:MOVE {}
    } -result {
        s {required value}
    }
    
    test MOVE-1.2 {location is not a mapref} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
    } -body {
        ted order -reject ABSIT:MOVE s 1 location NONESUCH
    } -cleanup {
        cleanup
    } -result {
        location {Invalid MGRS string: "NONESUCH"}
    }

    test MOVE-1.3 {location is out of neighborhood} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
        rdb eval {UPDATE absits SET state='ONGOING' WHERE s=1}
    } -body {
        ted order -reject ABSIT:MOVE s 1 location 38TNM000000
    } -cleanup {
        cleanup
    } -result {
        location {Cannot remove situation from its neighborhood}
    }


    test MOVE-2.1 {absit is updated} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
        set loc [latlong tomgrs [nbhood get NB1 refpoint]]
    } -body {
        ted order ABSIT:MOVE s 1 location $loc

        set newloc [latlong tomgrs \
            [rdb onecolumn {SELECT location FROM absits}]]
        expr {$loc == $newloc}
    } -cleanup {
        cleanup
    } -result {1}


    test MOVE-2.2 {table monitor event} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
        set loc [latlong tomgrs [nbhood randloc NB1]]
    } -body {
        ted notifier bind ::adb <absits>
        ted order ABSIT:MOVE s 1 location $loc
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::adb <absits> update 1}
    }


    test MOVE-3.1 {undo undoes the change} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
        set loc [latlong tomgrs [nbhood randloc NB1]]
        ted order ABSIT:MOVE s 1 location  $loc
    } -body {
        ted notifier bind ::adb <absits>
        adb order undo
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::adb <absits> update 1}
    }


    #-------------------------------------------------------------------
    # ABSIT:RESOLVE

    # 1.*: parameter type validation
    # 2.*: correct operation
    # 3.*: undo

    test RESOLVE-1.1 {required parms} -body {
        ted order -reject ABSIT:RESOLVE {}
    } -result {
        s {required value}
    }
    
    test RESOLVE-1.2 {s is not a live situation} -body {
        ted order -reject ABSIT:RESOLVE s 1
    } -cleanup {
        cleanup
    } -result {
        s {not a "live" situation: "1".}
    }


    test RESOLVE-1.3 {invalid resolver} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
    } -body {
        ted order -reject ABSIT:RESOLVE s 1 resolver NONESUCH
    } -cleanup {
        cleanup
    } -result {
        resolver {Invalid group "NONESUCH", should be one of: NONE, BLUE}
    }


    test RESOLVE-2.1 {absit is resolved} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
    } -body {
        ted order ABSIT:RESOLVE s 1 resolver BLUE

        ted querylist {SELECT * FROM absits}
    } -cleanup {
        cleanup
    } -match glob -result {
s          1
stype      BADWATER
n          NB1
coverage   1.0
inception  1
resolver   BLUE
rduration  0
state      RESOLVED
ts         0
tr         0
location   *
    }


    test RESOLVE-2.2 {absit is resolved, resolver is omitted} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
    } -body {
        ted order ABSIT:RESOLVE s 1

        ted querylist {SELECT * FROM absits}
    } -cleanup {
        cleanup
    } -match glob -result {
s          1
stype      BADWATER
n          NB1
coverage   1.0
inception  1
resolver   NONE
rduration  0
state      RESOLVED
ts         0
tr         0
location   *
    }


    test RESOLVE-2.3 {table monitor event} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
    } -body {
        ted notifier bind ::adb <absits>
        ted order ABSIT:RESOLVE s 1
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::adb <absits> update 1}
    }


    test RESOLVE-3.1 {undo undoes the change} -setup {
        ted create NB1 BLUE
        ted order ABSIT:CREATE $s1
        ted order ABSIT:RESOLVE s 1 resolver BLUE
    } -body {
        ted notifier bind ::adb <absits>
        adb order undo
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::adb <absits> update 1}
    }

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::










