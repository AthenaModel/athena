# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-TACTIC-EXECUTIVE.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    TACTIC:EXECUTIVE:* Orders tests
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    proc setup {} {
        ted create NB1
        variable S1
        variable B1
        variable T1
        ted order STRATEGY:BLOCK:ADD agent SYSTEM
        set S1 [strategy getname SYSTEM]
        lassign [$S1 blocks] B1
        ted order BLOCK:TACTIC:ADD block_id [$B1 id] typename EXECUTIVE
        lassign [$B1 tactics] T1
    }

    # Clean up after a test

    proc cleanup {} {
        ted cleanup
    }

    #-------------------------------------------------------------------
    # TACTIC:EXECUTIVE

    test TACTIC:EXECUTIVE-1.1 {required parameters} -setup {
        setup
    } -body {
        ted order -reject TACTIC:EXECUTIVE
    } -cleanup {
        cleanup
    } -result {
        tactic_id {required value}
    }

    test TACTIC:EXECUTIVE-1.2 {unknown tactic_id} -setup {
        setup
    } -body {
        ted order -reject TACTIC:EXECUTIVE tactic_id NONESUCH
    } -cleanup {
        cleanup
    } -match glob -result {
        tactic_id {Invalid EXECUTIVE ID: "NONESUCH"}
    }

    test TACTIC:EXECUTIVE-1.3 {script is in error} -setup {
        setup
    } -body {
        ted order -reject TACTIC:EXECUTIVE tactic_id [$T1 id] \
            command "foo \{"
    } -cleanup {
        cleanup
    } -result {
        command {Script is incomplete; check braces and quotes.}
    }

    test TACTIC:EXECUTIVE-2.1 {tactic updated} -setup {
        setup
    } -body {
        ted order TACTIC:EXECUTIVE \
            tactic_id [$T1 id] \
            command "Hello"

        ted pdicts [$T1 getdict]
    } -cleanup {
        cleanup
    } -match dictglob -result {
        command    Hello
        execstatus NONE
        faildict   {}
        id         *
        parent     *
        state      normal
    }

    test TACTIC:EXECUTIVE-2.2 {notifier event} -setup {
        setup
    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        ted order TACTIC:EXECUTIVE tactic_id [$T1 id] command "Hello"
        ted notifier received
    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}

    test TACTIC:EXECUTIVE-2.3 {undo notifier event} -setup {
        setup
        ted order TACTIC:EXECUTIVE tactic_id [$T1 id] command "Hello"
    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}
 
    test TACTIC:EXECUTIVE-3.1 {identity} -setup {
        setup
    } -body {
        ted tactic identity $T1
    } -cleanup {
        cleanup
    } -result {OK}

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::






