# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-nbhood.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    nbhood(sim) tests.
#
#    This test suite tests the nbhood(sim) mutators and queries
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*
    
    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        array unset parms
        ted cleanup
    }

    #-------------------------------------------------------------------
    # mutate create

    test create-1.1 {nbhood is created} -body {
        nbhood create [ted entity NB1]
        ted querylist {SELECT * FROM nbhoods}
    } -cleanup {
        cleanup
    } -result {
n               NB1
longname        Here
local           1
stacking_order  1
urbanization    URBAN
controller      
pcf             1.0
refpoint        1 1
polygon         0 0 2 0 2 2 0 2
obscured_by     
    }

    test create-1.2 {actor is set} -setup {
        ted create JOE
    } -body {
        
        nbhood create \
            [dict merge [ted entity NB1] {controller JOE}]
        ted querylist {SELECT * FROM nbhoods}
    } -cleanup {
        cleanup
    } -result {
n               NB1
longname        Here
local           1
stacking_order  1
urbanization    URBAN
controller      JOE
pcf             1.0
refpoint        1 1
polygon         0 0 2 0 2 2 0 2
obscured_by     
    }

    test create-1.3 {stacking_order is set} -body {
        nbhood create [ted entity NB1]
        nbhood create [ted entity NB2]

        ted querylist {SELECT n,stacking_order FROM nbhoods}
    } -cleanup {
        cleanup
    } -result {
n               NB1
stacking_order  1

n               NB2
stacking_order  2
    }


    test create-1.4 {obscured_by is set} -body {
        nbhood create [ted entity NB1]
        nbhood create [ted entity OV1]
        ted querylist {SELECT n,stacking_order,obscured_by FROM nbhoods}
    } -cleanup {
        cleanup
    } -result {
n               NB1
stacking_order  1
obscured_by     OV1

n               OV1
stacking_order  2
obscured_by     
    }

    test create-2.1 {undo deletes the nbhood} -setup {
        nbhood create [ted entity NB1]
    } -body {
        set undo [nbhood create [ted entity NB2]]
        namespace eval :: $undo

        # Is NB2 gone?
        adb eval {SELECT n FROM nbhoods}
    } -cleanup {
        cleanup
    } -result {NB1}


    test create-2.2 {undo updates obscured_by} -setup {
        nbhood create [ted entity NB1]
        set undo [nbhood create [ted entity OV1]]
    } -body {
        namespace eval :: $undo
        adb eval "SELECT n,obscured_by FROM nbhoods"
    } -cleanup {
        cleanup
    } -result {NB1 {}}

    test create-3.1 {nbrel_mn records are created} -body {
        nbhood create [ted entity NB1]
        nbhood create [ted entity NB2]
        ted query {SELECT * FROM nbrel_mn}
    } -cleanup {
        cleanup
    } -result {
m   n   proximity 
--- --- --------- 
NB1 NB1 HERE      
NB2 NB1 REMOTE    
NB1 NB2 REMOTE    
NB2 NB2 HERE      
    }

    test create-3.2 {undo deletes the nbrel_mn records} -setup {
        nbhood create [ted entity NB1]
    } -body {
        set undo [nbhood create [ted entity NB2]]
        namespace eval :: $undo

        # Is NB2 gone?
        ted query {SELECT * FROM nbrel_mn}
    } -cleanup {
        cleanup
    } -result {
m   n   proximity 
--- --- --------- 
NB1 NB1 HERE      
    }

    # create-4.*, demog_n effects, deleted as obsolete.

    test create-5.1 {econ_n records are created} -body {
        nbhood create [ted entity NB1]
        adb eval {SELECT n FROM econ_n}
    } -cleanup {
        cleanup
    } -result {NB1}

    test create-5.2 {undo deletes the econ_n record} -setup {
        nbhood create [ted entity NB1]
    } -body {
        set undo [nbhood create [ted entity NB2]]
        namespace eval :: $undo

        # Is NB2 gone?
        adb eval {SELECT n FROM econ_n}
    } -cleanup {
        cleanup
    } -result {NB1}


    #-------------------------------------------------------------------
    # mutate delete

    test delete-1.1 {Nbhood is deleted} -setup {
        ted create NB1 NB2
    } -body {
        nbhood delete NB1

        # Is NB1 gone?
        adb eval {SELECT n FROM nbhoods}
    } -cleanup {
        cleanup
    } -result {NB2}


    test delete-1.2 {obscured_by is cleared} -setup {
        ted create NB1 OV1
    } -body {
        nbhood delete OV1
        ted query {SELECT n,stacking_order,obscured_by FROM nbhoods}
    } -cleanup {
        cleanup
    } -result {
n   stacking_order obscured_by 
--- -------------- ----------- 
NB1 1                          
    }

    test delete-2.1 {Undo restores nbhood} -setup {
        ted create NB1
        set undo [nbhood delete NB1]
    } -body {
        namespace eval :: $undo
        ted querylist {SELECT * FROM nbhoods WHERE n='NB1'}
    } -cleanup {
        cleanup
    } -result {
n               NB1
longname        Here
local           1
stacking_order  1
urbanization    URBAN
controller      
pcf             1.0
refpoint        1 1
polygon         0 0 2 0 2 2 0 2
obscured_by     
    }


    test delete-2.2 {Undo restores stacking order} -setup {
        ted create NB1
        ted create OV1
        set undo [nbhood delete NB1]
    } -body {
        namespace eval :: $undo
        ted query {SELECT n,stacking_order,obscured_by FROM nbhoods}
    } -cleanup {
        cleanup
    } -result {
n   stacking_order obscured_by 
--- -------------- ----------- 
OV1 2                          
NB1 1              OV1         
    }

    test delete-3.1 {nbrel_mn records are deleted} -setup {
        ted create NB1 NB2
    } -body {
        nbhood delete NB2
        ted query {SELECT * FROM nbrel_mn}
    } -cleanup {
        cleanup
    } -result {
m   n   proximity 
--- --- --------- 
NB1 NB1 HERE      
    }

    test delete-3.2 {Undo restores nbrel_mn} -setup {
        ted create NB1
        set undo [nbhood delete NB1]
    } -body {
        namespace eval :: $undo
        ted query {SELECT * FROM nbrel_mn ORDER BY m,n}
    } -cleanup {
        cleanup
    } -result {
m   n   proximity 
--- --- --------- 
NB1 NB1 HERE      
    }

    # delete-4.*, demog_n effects, deleted as obsolete.

    test delete-5.1 {econ_n records are deleted} -setup {
        ted create NB1 NB2
    } -body {
        nbhood delete NB2
        adb eval {SELECT n FROM econ_n}
    } -cleanup {
        cleanup
    } -result {NB1}

    test delete-5.2 {Undo restores econ_n} -setup {
        ted create NB1
        set undo [nbhood delete NB1]
    } -body {
        namespace eval :: $undo
        adb eval {SELECT n FROM econ_n ORDER BY n}
    } -cleanup {
        cleanup
    } -result {NB1}


    test delete-6.1 {resident civilian groups deleted} -setup {
        ted create NB1 SHIA
    } -body {
        nbhood delete NB1
        adb eval {SELECT g FROM civgroups}
    } -cleanup {
        cleanup
    } -result {}

    test delete-6.2 {Undo restores resident civgroups} -setup {
        ted create NB1 SHIA
        set undo [nbhood delete NB1]
    } -body {
        namespace eval :: $undo
        adb eval {SELECT g FROM civgroups}
    } -cleanup {
        cleanup
    } -result {SHIA}

    test delete-7.1 {cap_kn records deleted} -setup {
        ted create CBS
    } -body {
        nbhood delete NB1
        adb eval {SELECT k,n FROM cap_kn}
    } -cleanup {
        cleanup
    } -result {}

    test delete-7.2 {Undo restores cap_kn records} -setup {
        ted create CBS
        set undo [nbhood delete NB1]
    } -body {
        namespace eval :: $undo
        adb eval {SELECT k,n FROM cap_kn}
    } -cleanup {
        cleanup
    } -result {CBS NB1}



    #-------------------------------------------------------------------
    # mutate lower

    test lower-1.1 {Nbhood is lowered} -setup {
        ted create NB1 OV1
    } -body {
        nbhood lower OV1
        ted query {SELECT n,stacking_order,obscured_by FROM nbhoods}
    } -cleanup {
        cleanup
    } -result {
n   stacking_order obscured_by 
--- -------------- ----------- 
NB1 2                          
OV1 1              NB1         
    }


    test lower-1.2 {<Stack>} -setup {
        ted create NB1 OV1
    } -body {
        ted notifier bind ::adb.nbhood <Stack>
        nbhood lower OV1
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::adb.nbhood <Stack>}
    }


    test lower-2.1 {Undo} -setup {
        ted create NB1 OV1
    } -body {
        set before [ted querylist {SELECT * FROM nbhoods}]

        set undo [nbhood lower OV1]

        namespace eval :: $undo

        set after  [ted querylist {SELECT * FROM nbhoods}]

        expr {$before eq $after}
    } -cleanup {
        cleanup
    } -result {1}

    #-------------------------------------------------------------------
    # mutate raise

    test raise-1.1 {Nbhood is raised} -setup {
        ted create NB1 OV1
    } -body {
        nbhood raise NB1
        ted query {SELECT n,stacking_order,obscured_by FROM nbhoods}
    } -cleanup {
        cleanup
    } -result {
n   stacking_order obscured_by 
--- -------------- ----------- 
NB1 2                          
OV1 1              NB1         
    }


    test raise-1.2 {<Stack>} -setup {
        ted create NB1 OV1
    } -body {
        ted notifier bind ::adb.nbhood <Stack>
        nbhood raise NB1
        ted notifier received
    } -cleanup {
        cleanup
    } -result {
        {::adb.nbhood <Stack>}
    }


    test raise-2.1 {Undo} -setup {
        ted create NB1 OV1
    } -body {
        set before [ted querylist {SELECT * FROM nbhoods}]

        set undo [nbhood raise NB1]

        namespace eval :: $undo

        set after  [ted querylist {SELECT * FROM nbhoods}]

        expr {$before eq $after}
    } -cleanup {
        cleanup
    } -result {1}

    #-------------------------------------------------------------------
    # mutate update

    test update-1.1 {nbhood is updated} -setup {
        ted create JOE NB1 
    } -body {
        nbhood update {
            n            NB1
            longname     "Not Here"
            local        0
            urbanization RURAL
            controller   JOE
            refpoint     {5 5}
            polygon      {0 0 10 0 10 10 0 10}
        }

        ted querylist {SELECT * FROM nbhoods}
    } -cleanup {
        cleanup
    } -result {
n               NB1
longname        Not Here
local           0
stacking_order  1
urbanization    RURAL
controller      JOE
pcf             1.0
refpoint        5 5
polygon         0 0 10 0 10 10 0 10
obscured_by     
    }


    test update-1.2 {Parameters can be empty} -setup {
        ted create NB1
    } -body {
        set before [ted querylist {SELECT * FROM nbhoods}]

        nbhood update {
            n            NB1
            longname     ""
            local        ""
            urbanization ""
            controller   ""
            refpoint     ""
            polygon      ""
        }

        set after [ted querylist {SELECT * FROM nbhoods}]

        expr {$before eq $after}
    } -cleanup {
        cleanup
    } -result {1}


    test update-1.3 {controller NONE yields NULL} -setup {
        ted create JOE NB1 
        nbhood update {
            n            NB1
            longname     ""
            local        ""
            urbanization ""
            controller   JOE
            refpoint     ""
            polygon      ""
        }
    } -body {
        nbhood update {
            n            NB1
            longname     ""
            local        ""
            urbanization ""
            controller   NONE
            refpoint     ""
            polygon      ""
        }

        ted querylist {SELECT * FROM nbhoods}
    } -cleanup {
        cleanup
    } -result {
n               NB1
longname        Here
local           1
stacking_order  1
urbanization    URBAN
controller      
pcf             1.0
refpoint        1 1
polygon         0 0 2 0 2 2 0 2
obscured_by     
    }

    test update-1.4 {stacking_order,obscured_by are set} -setup {
        ted create NB1 NB2
    } -body {
        # Move NB2 over NB1
        nbhood update [ted entity OV1 n NB2 longname "There"]

        ted query {SELECT n,stacking_order,obscured_by FROM nbhoods}
    } -cleanup {
        cleanup
    } -result {
n   stacking_order obscured_by 
--- -------------- ----------- 
NB1 1              NB2         
NB2 2                          
    }


    test update-2.1 {undo undoes the change} -setup {
        ted create NB1
    } -body {
        set before [ted querylist {SELECT * FROM nbhoods}]

        set undo [nbhood update [ted entity NB2 n NB1]]

        namespace eval :: $undo

        set after [ted querylist {SELECT * FROM nbhoods}]

        expr {$before eq $after}
    } -cleanup {
        cleanup
    } -result {1}


    test update-2.2 {stacking_order,obscured_by are undone} -setup {
        ted create NB1 NB2
    } -body {
        set before [ted querylist {SELECT * FROM nbhoods}]

        # Move NB2 over NB1
        set undo [nbhood update [ted entity OV1 n NB2]]
        
        namespace eval :: $undo

        set after [ted querylist {SELECT * FROM nbhoods}]

        expr {$before eq $after}
    } -cleanup {
        cleanup
    } -result {1}


    #-------------------------------------------------------------------
    # names

    test names-1.1 {Returns neighborhood names: none defined} -body {
        nbhood names
    } -cleanup {
        cleanup
    } -result {}

    test names-1.2 {Returns neighborhood names} -setup {
        ted create NB1 NB2
    } -body {
        nbhood names
    } -cleanup {
        cleanup
    } -result {NB1 NB2}

    #-------------------------------------------------------------------
    # names

    test local_names-1.1 {Returns local nbhood names: none defined} -body {
        nbhood local names
    } -cleanup {
        cleanup
    } -result {}

    test local_names-1.2 {Returns only local neighborhood names} -setup {
        ted create NB1 NB2 NL1
    } -body {
        nbhood local names
    } -cleanup {
        cleanup
    } -result {NB1 NB2}

    #-------------------------------------------------------------------
    # find

    test find-1.1 {No neighborhood to find} -body {
        nbhood find 1 1
    } -result {}
    

    test find-1.2 {Find stacked neighborhoods} -setup {
        nbhood create {
            n            N1
            longname     "Nb 1"
            local        1
            urbanization RURAL
            refpoint     {2 2}
            polygon      {0 0 10 0 10 10 0 10}
        }

        nbhood create {
            n            N2
            longname     "Nb 2"
            local        1
            urbanization RURAL
            refpoint     {5 5}
            polygon      {3 3 7 3 7 7 3 7}
        }
    } -body {
        set a [nbhood find 1 1]  ;# Should be N1
        set b [nbhood find 5 5]  ;# Should be N2

        nbhood lower N2

        set c [nbhood find 5 5]  ;# Should be N1

        nbhood raise N2

        set d [nbhood find 5 5]  ;# Should be N2

        list $a $b $c $d
    } -cleanup {
        cleanup
    } -result {N1 N2 N1 N2}

    #-------------------------------------------------------------------
    # validate

    test validate-1.1 {Invalid name: none defined} -body {
        nbhood validate NONESUCH
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid neighborhood, none are defined}

    test validate-1.2 {Invalid name: some defined} -setup {
        ted create NB1 NB2
    } -body {
        nbhood validate NONESUCH
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid neighborhood, should be one of: NB1, NB2}

    test validate-2.1 {Valid name} -setup {
        ted create NB1 NB2
    } -body {
        nbhood validate NB1
    } -cleanup {
        cleanup
    } -result {NB1}

    #-------------------------------------------------------------------
    # local validate

    test local_validate-1.1 {Invalid local name: none defined} -body {
        nbhood local validate NONESUCH
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid local neighborhood, none are defined}

    test local_validate-1.2 {Invalid local name: some defined} -setup {
        ted create NB1 NB2 NL1
    } -body {
        nbhood local validate NONESUCH
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid local neighborhood, should be one of: NB1, NB2}

    test local_validate-1.3 {Non-local nbhood: some defined} -setup {
        ted create NB1 NB2 NL1
    } -body {
        nbhood local validate NL1
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid local neighborhood, should be one of: NB1, NB2}

    test local_validate-2.1 {Valid name} -setup {
        ted create NB1 NB2 NL1
    } -body {
        nbhood local validate NB1
    } -cleanup {
        cleanup
    } -result {NB1}

    #-------------------------------------------------------------------
    # dbsync

    # This is effectively tested by the lower and raise tests.
    

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::








