# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-demog.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    demog(sim) tests.
#
#    This test suite tests the demog(sim) analysis and queries
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # setup ?flag?
    #
    # flag  - yes or no
    #
    # Sets up for tests; by default, sets econ enabled.
    # Pass state explicitly to disable it.
    proc setup {{state ENABLED}} {
        econ setstate $state
        ted create JOE BOB BLUE USAID NB1 NB2 SHIA SUNN KURD
        ted lock
    }
    
    # Clean up after a test
    proc cleanup {} {
        # Clean up the test environment
        ted cleanup
    }

    #-------------------------------------------------------------------
    # start
    #
    # NOTE: This tests [tdb demog stats] as well

    test start-1.1 {demog_g is populated} -setup {
        ted create JOE BOB BLUE USAID NB1 NB2 SHIA SUNN KURD
    } -body {
        tdb demog start
        ted querylist {SELECT * FROM demog_g}
    } -cleanup {
        cleanup
    } -result {
g            SHIA
population   1000
real_pop     1000.0
subsistence  0
consumers    1000
labor_force  600
employed     0
unemployed   0
ur           0.0
upc          0.0
uaf          0.0
tc           0.0
aloc         0.0
rloc         0.0
eloc         0.0
povfrac      0.0
attrition    0

g            SUNN
population   1000
real_pop     1000.0
subsistence  0
consumers    1000
labor_force  600
employed     0
unemployed   0
ur           0.0
upc          0.0
uaf          0.0
tc           0.0
aloc         0.0
rloc         0.0
eloc         0.0
povfrac      0.0
attrition    0

g            KURD
population   1000
real_pop     1000.0
subsistence  1000
consumers    0
labor_force  0
employed     0
unemployed   0
ur           0.0
upc          0.0
uaf          0.0
tc           0.0
aloc         0.0
rloc         0.0
eloc         0.0
povfrac      0.0
attrition    0
    }

    test start-1.2 {demog_n is populated} -setup {
        ted create JOE BOB BLUE USAID NB1 NB2 SHIA SUNN KURD
    } -body {
        tdb demog start
        ted query {SELECT * FROM demog_n}
    } -cleanup {
        cleanup
    } -result {
n   population subsistence consumers labor_force unemployed ur  upc uaf 
--- ---------- ----------- --------- ----------- ---------- --- --- --- 
NB1 2000       0           2000      1200        0          0.0 0.0 0.0 
NB2 1000       1000        0         0           0          0.0 0.0 0.0 
    }

    #-------------------------------------------------------------------
    # getg

    test getg-1.1 {can get all data} -setup {
        setup DISABLED
    } -body {
        tdb demog getg SHIA
    } -cleanup {
        cleanup
    } -result {subsistence 0 povfrac 0.0 unemployed 24 eloc 0.0 population 1000 uaf 0.0 attrition 0 labor_force 600 tc 0.0 g SHIA consumers 1000 employed 576 rloc 0.0 upc 2.4 real_pop 1000.0 aloc 0.0 ur 4.0}
    
    test getg-1.2 {can get one parameter} -setup {
        setup
    } -body {
        tdb demog getg SHIA labor_force
    } -cleanup {
        cleanup
    } -result {600}
    
    test getg-1.3 {returns "" if not found} -setup {
        setup
    } -body {
        tdb demog getg NONESUCH
    } -cleanup {
        cleanup
    } -result {}
    
    #-------------------------------------------------------------------
    # getn

    test getn-1.1 {can get all data} -setup {
        setup
        # Remove any dependancy on econ model
        adb eval {
            UPDATE demog_n SET uaf=0.0,upc=2.4,unemployed=48 WHERE n='NB1'
        }
    } -body {
        tdb demog getn NB1
    } -cleanup {
        cleanup
    } -result {uaf 0.0 upc 2.4 subsistence 0 unemployed 48 labor_force 1200 n NB1 ur 4.0 consumers 2000 population 2000}
    
    test getn-1.2 {can get one parameter} -setup {
        setup
    } -body {
        tdb demog getn NB1 labor_force
    } -cleanup {
        cleanup
    } -result {1200}
    
    test getn-1.3 {returns "" if not found} -setup {
        setup
    } -body {
        tdb demog getn NONESUCH
    } -cleanup {
        cleanup
    } -result {}
    
    #-------------------------------------------------------------------
    # getlocal

    test getlocal-1.1 {can get all data} -setup {
        setup
    } -body {
        tdb demog getlocal
    } -cleanup {
        cleanup
    } -result {labor_force 1200 consumers 2000 population 3000}
    
    test getlocal-1.2 {can get one parameter} -setup {
        setup
    } -body {
        tdb demog getlocal labor_force
    } -cleanup {
        cleanup
    } -result {1200}


    #-------------------------------------------------------------------
    # pop

    test pop-1.1 {get pop for multiple neighborhoods} -setup {
        setup
    } -body {
        tdb demog pop {NB1 NB2}
    } -cleanup {
        cleanup
    } -result {3000}

    #-------------------------------------------------------------------
    # shares

    test shares-1.1 {get shares for multiple neighborhoods} -setup {
        setup
    } -body {
        tdb demog shares {NB1 NB2}
    } -cleanup {
        cleanup
    } -result {NB1 0.6666666666666666 NB2 0.3333333333333333}

    test shares-1.2 {get shares for empty neighborhoods} -setup {
        setup
        adb eval {UPDATE demog_n SET population = 0.0}
    } -body {
        tdb demog shares {NB1 NB2}
    } -cleanup {
        cleanup
    } -result {NB1 0.0 NB2 0.0}
    

    #-------------------------------------------------------------------
    # adjust

    test adjust-1.1 {adds to population} -setup {
        setup
    } -body {
        tdb demog adjust SHIA 100
        tdb demog getg SHIA population
    } -cleanup {
        cleanup
    } -result {1100}
    
    test adjust-1.2 {subtracts from population} -setup {
        setup
    } -body {
        tdb demog adjust SHIA -100
        tdb demog getg SHIA population
    } -cleanup {
        cleanup
    } -result {900}
    
    test adjust-1.3 {subtracts but not less than 0} -setup {
        setup
    } -body {
        tdb demog adjust SHIA -1100
        tdb demog getg SHIA population
    } -cleanup {
        cleanup
    } -result {0}
    
    test adjust-1.4 {change can be fractional} -setup {
        setup
    } -body {
        tdb demog adjust SHIA 0.5
        list [tdb demog getg SHIA population] [tdb demog getg SHIA real_pop]
    } -cleanup {
        cleanup
    } -result {1001 1000.5}

    test adjust-1.5 {fractional changes accumulate} -setup {
        setup
    } -body {
        tdb demog adjust SHIA 0.7
        tdb demog adjust SHIA 0.7
        format "%d %.1f" [tdb demog getg SHIA population] [tdb demog getg SHIA real_pop]
    } -cleanup {
        cleanup
    } -result {1001 1001.4}

    #-------------------------------------------------------------------
    # attrit

    test attrit-1.1 {subtracts from population} -setup {
        setup
    } -body {
        tdb demog attrit SHIA 100
        list [tdb demog getg SHIA population] [tdb demog getg SHIA attrition]
    } -cleanup {
        cleanup
    } -result {900 100}
    
    test attrit-1.2 {attrition is accumulated} -setup {
        setup
    } -body {
        tdb demog attrit SHIA 100
        tdb demog attrit SHIA 200
        list [tdb demog getg SHIA population] [tdb demog getg SHIA attrition]
    } -cleanup {
        cleanup
    } -result {700 300}

    test attrit-1.3 {subtracts but not less than 0} -setup {
        setup
    } -body {
        tdb demog attrit SHIA 1100
        list [tdb demog getg SHIA population] [tdb demog getg SHIA attrition]
    } -cleanup {
        cleanup
    } -result {0 1000}
    
    #-------------------------------------------------------------------
    # growth

    test growth-1.1 {changes population size} -setup {
        setup
        adb eval {UPDATE civgroups SET pop_cr = 100.0}
    } -body {
        tdb demog growth
        ted query {SELECT g, population, real_pop FROM demog_g}
    } -cleanup {
        cleanup
    } -result {
g    population real_pop           
---- ---------- ------------------ 
SHIA 1019       1019.2307692307693 
SUNN 1019       1019.2307692307693 
KURD 1019       1019.2307692307693 
    }
    
    test growth-1.2 {can be negative} -setup {
        setup
        adb eval {UPDATE civgroups SET pop_cr = -100.0}
    } -body {
        tdb demog growth
        ted query {SELECT g, population, real_pop FROM demog_g}
    } -cleanup {
        cleanup
    } -result {
g    population real_pop          
---- ---------- ----------------- 
SHIA 981        980.7692307692307 
SUNN 981        980.7692307692307 
KURD 981        980.7692307692307 
    }
       
    
    #-------------------------------------------------------------------
    # econstats
    test econstats-1.1 {More workers, more unemployment} -setup {
        setup
        adb eval {UPDATE demog_n SET labor_force = 2000 WHERE n='NB1'}
        tdb demog econstats
    } -body {
        set a [adb eval {SELECT ur FROM demog_n WHERE n='NB1'}]
        adb eval {UPDATE demog_n SET labor_force = 3000 WHERE n='NB1'}
        tdb demog econstats
        set b [adb eval {SELECT ur FROM demog_n WHERE n='NB1'}]
        expr {$a < $b}
    } -cleanup {
        cleanup
    } -result {1}

    test econstats-1.2 {Fewer workers, less unemployment} -setup {
        setup
        adb eval {UPDATE demog_n SET labor_force = 2000 WHERE n='NB1'}
        tdb demog econstats
    } -body {
        set a [adb eval {SELECT ur FROM demog_n WHERE n='NB1'}]
        adb eval {UPDATE demog_n SET labor_force = 1000 WHERE n='NB1'}
        tdb demog econstats
        set b [adb eval {SELECT ur FROM demog_n WHERE n='NB1'}]
        expr {$a > $b}
    } -cleanup {
        cleanup
    } -result {1}

    test econstats-1.3 {Workers willing to travel, less unemployment} -setup {
        econ setstate ENABLED

        ted create JOE BOB BLUE USAID NB1 NB2 SHIA SUNN KURD 
        parmdb set demog.maxcommute NEAR
        adb eval {UPDATE nbrel_mn SET proximity='REMOTE'}
        ted lock
    } -body {
        set a [adb eval {SELECT ur FROM demog_n WHERE n='NB1'}]
        # Make NB1 and NB2 NEAR to each other
        adb eval {UPDATE nbrel_mn SET proximity='NEAR'}
        tdb demog econstats
        set b [adb eval {SELECT ur FROM demog_n WHERE n='NB1'}]
        expr {$a > $b}
    } -cleanup {
        cleanup
    } -result {1}

    test econstats-1.4 {Workers won't travel, more unemployment} -setup {
        econ setstate ENABLED
        ted create JOE BOB BLUE USAID NB1 NB2 SHIA SUNN KURD 
        parmdb set demog.maxcommute NEAR 
        adb eval {UPDATE nbrel_mn SET proximity='NEAR'}
        ted lock
    } -body {
        set a [adb eval {SELECT ur FROM demog_n WHERE n='NB1'}]
        # Make NB1 and NB2 FAR to each other
        adb eval {UPDATE nbrel_mn SET proximity='FAR'}
        tdb demog econstats
        set b [adb eval {SELECT ur FROM demog_n WHERE n='NB1'}]
        expr {$a < $b}
    } -cleanup {
        cleanup
    } -result {1}

    #-------------------------------------------------------------------
    # gIn

    test gIn-1.1 {Groups resident in n, PREP} -setup {
        ted create NB1 NB2 BLUE SUNN SHIA KURD
    } -body {
        lsort [tdb demog gIn NB1]
    } -cleanup {
        cleanup
    } -result {SHIA SUNN}
    
    test gIn-1.2 {Groups resident in n, lock} -setup {
        ted create NB1 NB2 BLUE SUNN SHIA KURD
        ted lock
    } -body {
        lsort [tdb demog gIn NB1]
    } -cleanup {
        cleanup
    } -result {SHIA SUNN}

    test gIn-2.1 {Skips empty groups, PREP} -setup {
        ted create NB1 NB2 BLUE SUNN SHIA KURD
        adb eval {UPDATE civgroups SET basepop = 0 WHERE g='SHIA'}
    } -body {
        lsort [tdb demog gIn NB1]
    } -cleanup {
        cleanup
    } -result {SUNN}
    
    test gIn-2.2 {Skips empty groups, lock} -setup {
        ted create NB1 NB2 BLUE SUNN SHIA KURD
        ted lock
        adb eval {UPDATE demog_g SET population = 0 WHERE g='SUNN'}
    } -body {
        lsort [tdb demog gIn NB1]
    } -cleanup {
        cleanup
    } -result {SHIA}

    #-------------------------------------------------------------------
    # saIn

    test saIn-1.1 {SA Groups resident in n, PREP} -setup {
        ted create NB1 NB2 BLUE SUNN SHIA KURD
        adb eval {UPDATE civgroups SET sa_flag=1 WHERE g='SUNN'}
    } -body {
        lsort [tdb demog saIn NB1]
    } -cleanup {
        cleanup
    } -result {SUNN}
    
    test saIn-1.2 {SA Groups resident in n, lock} -setup {
        ted create NB1 NB2 BLUE SUNN SHIA KURD
        adb eval {UPDATE civgroups SET sa_flag=1 WHERE g='SUNN'}
        ted lock
    } -body {
        lsort [tdb demog saIn NB1]
    } -cleanup {
        cleanup
    } -result {SUNN}

    test saIn-2.1 {Skips empty groups, PREP} -setup {
        ted create NB1 NB2 BLUE SUNN SHIA KURD
        adb eval {UPDATE civgroups SET sa_flag=1}
        adb eval {UPDATE civgroups SET basepop = 0 WHERE g='SUNN'}
    } -body {
        lsort [tdb demog saIn NB1]
    } -cleanup {
        cleanup
    } -result {SHIA}
    
    test saIn-2.2 {Skips empty groups, lock} -setup {
        ted create NB1 NB2 BLUE SUNN SHIA KURD
        ted lock
        adb eval {UPDATE civgroups SET sa_flag=1}
        adb eval {UPDATE demog_g SET population = 0 WHERE g='SUNN'}
    } -body {
        lsort [tdb demog saIn NB1]
    } -cleanup {
        cleanup
    } -result {SHIA}

    #-------------------------------------------------------------------
    # nonSaIn

    test nonSaIn-1.1 {SA Groups resident in n, PREP} -setup {
        ted create NB1 NB2 BLUE SUNN SHIA KURD
        adb eval {UPDATE civgroups SET sa_flag=1 WHERE g='SUNN'}
    } -body {
        lsort [tdb demog nonSaIn NB1]
    } -cleanup {
        cleanup
    } -result {SHIA}
    
    test nonSaIn-1.2 {SA Groups resident in n, lock} -setup {
        ted create NB1 NB2 BLUE SUNN SHIA KURD
        adb eval {UPDATE civgroups SET sa_flag=1 WHERE g='SUNN'}
        ted lock
    } -body {
        lsort [tdb demog nonSaIn NB1]
    } -cleanup {
        cleanup
    } -result {SHIA}

    test nonSaIn-2.1 {Skips empty groups, PREP} -setup {
        ted create NB1 NB2 BLUE SUNN SHIA KURD
        adb eval {UPDATE civgroups SET basepop = 0 WHERE g='SUNN'}
    } -body {
        lsort [tdb demog nonSaIn NB1]
    } -cleanup {
        cleanup
    } -result {SHIA}
    
    test nonSaIn-2.2 {Skips empty groups, lock} -setup {
        ted create NB1 NB2 BLUE SUNN SHIA KURD
        ted lock
        adb eval {UPDATE demog_g SET population = 0 WHERE g='SUNN'}
    } -body {
        lsort [tdb demog nonSaIn NB1]
    } -cleanup {
        cleanup
    } -result {SHIA}

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





