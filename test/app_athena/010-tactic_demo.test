# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-tactic_demo.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    tactic_demo(sim) tests.
#
#    This test suite tests the tactic_demo(sim) class.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    # If opt is -valid, sets up a valid tactic.
    proc setup {{opt ""}} {
        variable B1
        variable T1
        variable coffer

        # FIRST, create the basic entities.
        ted create JOE BLUE ALQ NB1 NB2 SUNN SHIA KURD

        # NEXT, create a default DEMO tactic
        set B1 [ted addblock SYSTEM]
        set T1 [ted addtactic $B1 DEMO]

        # NEXT, prepare for obligation
        personnel start
        personnel load
        set coffer [::athena::coffer new [::adb athenadb] JOE]
    }

    proc cleanup {} {
        variable coffer
        $coffer destroy
        strategy locking 0
        ted cleanup
    }

    #-------------------------------------------------------------------
    # Constructor

    test constructor-1.1 {default state} -setup {
        setup
    } -body {
        ted pdicts [$T1 getdict]
    } -cleanup {
        cleanup
    } -match dictglob -result {
        coverage   0.5
        execstatus NONE
        faildict   {}
        g          {}
        id         *
        n          {}
        name       T*
        parent     *
        pot        *::pot
        state      invalid
    }

    test constructor-1.2 {can set options} -setup {
        setup
    } -body {
        set T1 [::athena::tactic::DEMO new -parent DUMMY]
        $T1 get parent
    } -cleanup {
        cleanup
    } -result {DUMMY}

    #-------------------------------------------------------------------
    # narrative

    test narrative-1.1 {narrative, default state} -setup {
        setup
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {DEMO abstract event by ??? in ??? (cov=0.50).}

    test narrative-1.2 {narrative, specific settings} -setup {
        setup
        $T1 configure         \
            -n        NB1     \
            -g        SHIA    \
            -coverage 0.75
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {DEMO abstract event by {group:SHIA} in {nbhood:NB1} (cov=0.75).}

    #-------------------------------------------------------------------
    # check

    test check-1.1 {Initial state fails} -setup {
        setup
    } -body {
        ted pdicts [$T1 check]
    } -cleanup {
        cleanup
    } -match dict -result {
        n {No neighborhood selected.} 
        g {No demonstrating group selected.}
    }

    test check-1.2 {unknown n} -setup {
        setup 
    } -body {
        $T1 configure -n NONESUCH -g SHIA
        ted pdicts [$T1 check]
    } -cleanup {
        cleanup
    } -match dict -result {
        n {No such neighborhood: "NONESUCH".}
    }

    test check-1.3 {unknown g} -setup {
        setup 
    } -body {
        $T1 configure -n NB1 -g NONESUCH
        ted pdicts [$T1 check]
    } -cleanup {
        cleanup
    } -match dict -result {
        g {No such group: "NONESUCH".}
    }

    test check-1.4 {Success} -setup {
        setup
        $T1 configure -n NB1 -g SHIA 
    } -body {
        $T1 check
    } -cleanup {
        cleanup
    } -result {}

    #-------------------------------------------------------------------
    # obligate
    #

    test obligate-1.1 {obligate always works, no money} -setup {
        setup
        $T1 configure -n NB1 -g SHIA
    } -body {
        $T1 obligate $coffer
    } -cleanup {
        cleanup
    } -result {1}

    #-------------------------------------------------------------------
    # execute

    test execute-1.1 {creates demo} -setup {
        setup
        $T1 configure         \
            -n        NB1     \
            -g        SHIA    \
            -coverage 0.75
    } -body {
        $T1 execute
        ted pdicts [lindex [abevent pending] 0]
    } -cleanup {
        cleanup
    } -match dict -result {
        coverage 0.75
        dtype    DEMO
        g        SHIA
        n        NB1
    }

    test execute-1.2 {can create multiple} -setup {
        setup
        $T1 configure         \
            -n        NB1     \
            -g        SHIA    \
            -coverage 0.5
    } -body {
        $T1 execute
        $T1 execute
        llength [abevent pending]
    } -cleanup {
        cleanup
    } -result {2}

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::









