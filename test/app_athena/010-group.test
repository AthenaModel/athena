# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-group.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    group(sim) tests.
#
#    This test suite tests the group(sim) queries
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        array unset parms
        ted cleanup
    }

    #-------------------------------------------------------------------
    # names

    test names-1.1 {Returns names: none defined} -body {
        tdb group names
    } -cleanup {
        cleanup
    } -result {}

    test names-1.2 {Returns names} -setup {
        ted create BLUE SHIA USAID
    } -body {
        tdb group names
    } -cleanup {
        cleanup
    } -result {BLUE SHIA USAID}

    #-------------------------------------------------------------------
    # namedict

    test namedict-1.1 {Returns namedict: none defined} -body {
        tdb group namedict
    } -cleanup {
        cleanup
    } -result {}

    test namedict-1.2 {Returns namedict} -setup {
        ted create BLUE SHIA USAID
    } -body {
        ted pdicts [tdb group namedict]
    } -cleanup {
        cleanup
    } -match dict -result {
        BLUE  {US Army}
        SHIA  Shia
        USAID {US Aid}
    }

    #-------------------------------------------------------------------
    # validate

    test validate-1.1 {Invalid name: none defined} -body {
        tdb group validate NONESUCH
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid group, none are defined}

    test validate-1.2 {Invalid name: some defined} -setup {
        ted create BLUE SHIA USAID
    } -body {
        tdb group validate NONESUCH
    } -returnCodes {
        error
    } -cleanup {
        cleanup
    } -result {Invalid group, should be one of: BLUE, SHIA, USAID}

    test validate-2.1 {Valid name} -setup {
        ted create BLUE SHIA USAID
    } -body {
        tdb group validate BLUE
    } -cleanup {
        cleanup
    } -result {BLUE}

    #-------------------------------------------------------------------
    # exists

    # Tested by validate.
    

    #-------------------------------------------------------------------
    # gtype

    test gtype-1.1 {Invalid group} -setup {
        ted create BLUE SHIA USAID
    } -body {
        tdb group gtype NONESUCH
    } -cleanup {
        cleanup
    } -result {}

    test gtype-1.2 {Valid groups} -setup {
        ted create BLUE SHIA USAID
    } -body {
        list \
            [tdb group gtype SHIA]  \
            [tdb group gtype BLUE]  \
            [tdb group gtype USAID]
    } -cleanup {
        cleanup
    } -result {CIV FRC ORG}

    #-------------------------------------------------------------------
    # isLocal

    test isLocal-1.1 {Invalid group} -setup {
        ted create BLUE SHIA USAID
    } -body {
        tdb group isLocal NONESUCH
    } -cleanup {
        cleanup
    } -result {0}

    test isLocal-1.2 {Valid groups} -setup {
        ted create BLUE SHIA USAID
    } -body {
        list \
            [tdb group isLocal SHIA]  \
            [tdb group isLocal BLUE]  \
            [tdb group isLocal USAID]
    } -cleanup {
        cleanup
    } -result {1 0 0}

    #-------------------------------------------------------------------
    # ownedby

    test ownedby-1.1 {Invalid group} -setup {
        ted create BLUE SHIA USAID
    } -body {
        tdb group ownedby NONESUCH
    } -cleanup {
        cleanup
    } -result {}

    test ownedby-1.2 {Valid groups} -setup {
        ted create JOE BLUE SHIA USAID ALQ
    } -body {
        tdb group ownedby JOE
    } -cleanup {
        cleanup
    } -result {BLUE USAID}

    #-------------------------------------------------------------------
    # otherthan

    test otherthan-1.1 {no group} -setup {
        ted create BLUE BRIT SHIA USAID
    } -body {
        tdb group otherthan {}
    } -cleanup {
        cleanup
    } -result {BLUE BRIT SHIA USAID}

    test otherthan-1.2 {one group} -setup {
        ted create BLUE BRIT SHIA USAID
    } -body {
        tdb group otherthan SHIA
    } -cleanup {
        cleanup
    } -result {BLUE BRIT USAID}

    test otherthan-1.3 {multiple groups} -setup {
        ted create BLUE BRIT SHIA USAID
    } -body {
        tdb group otherthan {BLUE SHIA}
    } -cleanup {
        cleanup
    } -result {BRIT USAID}

    #-------------------------------------------------------------------
    # bsid

    test bsid-1.1 {group has own bsid} -setup {
        ted create BLUE
        adb eval {
            UPDATE groups SET bsid=5 WHERE g='BLUE'
        }
    } -body {
        tdb group bsid BLUE
    } -cleanup {
        cleanup
    } -result {5}

    test bsid-1.2 {group gets bsid from owner} -setup {
        ted create JOE BLUE
        adb eval {
            UPDATE actors SET bsid=7 WHERE a='JOE'
        }
    } -body {
        tdb group bsid BLUE
    } -cleanup {
        cleanup
    } -result {7}

    test bsid-1.3 {group gets default bsid} -setup {
        ted create JOE BLUE
        adb eval {
            UPDATE actors SET bsid=NULL WHERE a='JOE'
        }
    } -body {
        tdb group bsid BLUE
    } -cleanup {
        cleanup
    } -result {1}

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::

