# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-TACTIC-DEMOB.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    TACTIC:DEMOB:* Orders tests
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    proc setup {} {
        ted create JOE BLUE ALQ
        variable S1
        variable B1
        variable T1
        set S1 [strategy getname JOE]
        ted order STRATEGY:BLOCK:ADD agent JOE
        lassign [$S1 blocks] B1
        ted order BLOCK:TACTIC:ADD block_id [$B1 id] typename DEMOB
        lassign [$B1 tactics] T1
    }

    # Clean up after a test

    proc cleanup {} {
        ted cleanup
    }

    #-------------------------------------------------------------------
    # TACTIC:DEMOB

    test TACTIC:DEMOB-1.1 {required parameters} -setup {
        setup
    } -body {
        ted order -reject TACTIC:DEMOB
    } -cleanup {
        cleanup
    } -result {
        tactic_id {required value}
    }

    test TACTIC:DEMOB-1.2 {unknown tactic_id} -setup {
        setup
    } -body {
        ted order -reject TACTIC:DEMOB tactic_id NONESUCH
    } -cleanup {
        cleanup
    } -match glob -result {
        tactic_id {Invalid DEMOB ID: "NONESUCH"}
    }

    test TACTIC:DEMOB-1.3 {invalid g} -setup {
        setup
    } -body {
        ted order -reject TACTIC:DEMOB tactic_id [$T1 id] g "NONE SUCH"
    } -cleanup {
        cleanup
    } -result {
        g {Identifiers begin with a letter and contain only letters and digits.}
    }

    test TACTIC:DEMOB-1.4 {invalid personnel} -setup {
        setup
    } -body {
        ted order -reject TACTIC:DEMOB tactic_id [$T1 id] personnel NONESUCH
    } -cleanup {
        cleanup
    } -result {
        personnel {invalid value "NONESUCH", expected integer}
    }

    test TACTIC:DEMOB-1.5 {0 personnel in order} -setup {
        setup
    } -body {
        ted order -reject TACTIC:DEMOB tactic_id [$T1 id] \
            mode SOME personnel 0
    } -cleanup {
        cleanup
    } -result {
        personnel {Mode requires personnel greater than 0.}
    }

    test TACTIC:DEMOB-1.6 {0.0 percentage in tactic} -setup {
        setup
    } -body {
        ted order -reject TACTIC:DEMOB tactic_id [$T1 id] mode PERCENT
    } -cleanup {
        cleanup
    } -result {
        percent {Mode requires a percentage greater than 0.0%.}
    }

    test TACTIC:DEMOB-1.7 {invalid name} -setup {
        setup
    } -body {
        ted order -reject TACTIC:DEMOB tactic_id [$T1 id] name 1234
    } -cleanup {
        cleanup
    } -match glob -result {
        name {Identifiers begin with a letter and contain only letters and digits.}
    }

    test TACTIC:DEMOB-1.8 {duplicate name} -setup {
        setup
        set T2 [ted addtactic $B1 ABSIT]
    } -body {
        ted order -reject TACTIC:DEMOB tactic_id [$T1 id] \
            name [$T2 get name]
    } -cleanup {
        cleanup
    } -match glob -result {
        name {Name already exists: "T2"}
    }

    test TACTIC:DEMOB-2.1 {tactic updated} -setup {
        setup
    } -body {
        ted order TACTIC:DEMOB \
            tactic_id [$T1 id]         \
            g         BLUE             \
            mode      SOME             \
            personnel 1000             \
            percent   5.0

        ted sortdict [$T1 getdict]
    } -cleanup {
        cleanup
    } -match dictglob -result {
        execstatus NONE
        faildict   {}
        g          BLUE
        id         *
        mode       SOME
        name       T*
        parent     *
        percent    5.0
        personnel  1000
        pot        *::pot
        state      normal
    }

    test TACTIC:DEMOB-2.2 {notifier event} -setup {
        setup
    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        ted order TACTIC:DEMOB tactic_id [$T1 id] personnel 100
        ted notifier received
    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}

    test TACTIC:DEMOB-2.3 {undo notifier event} -setup {
        setup
        ted order TACTIC:DEMOB tactic_id [$T1 id] personnel 100
    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        adb order undo
        ted notifier received
    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}
 
    test TACTIC:DEMOB-3.1 {identity} -setup {
        setup
    } -body {
        ted tactic identity $T1
    } -cleanup {
        cleanup
    } -result {OK}

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::







