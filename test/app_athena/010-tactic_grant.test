# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-tactic_grant.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    tactic_grant(sim) tests.
#
#    This test suite tests the tactic_grant(sim) class.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    proc setup {} {
        variable T1
        variable coffer
        ted create JOE BOB CBS NBC ABC CNN FOX PBS
        cap access load
        set coffer [::athena::coffer new [::adb athenadb] JOE]

        set B1 [ted addblock JOE]
        set T1 [ted addtactic $B1 GRANT]
    }

    # Clean up after a test

    proc cleanup {} {
        variable coffer
        $coffer destroy
        ted cleanup
    }

    #-------------------------------------------------------------------
    # Constructor

    test constructor-1.1 {default state} -setup {
        setup
    } -body {
        ted pdicts [$T1 getdict]
    } -cleanup {
        cleanup
    } -match dictglob -result {
        alist      {_type ACTORS _rule {}}
        execstatus NONE
        faildict   {}
        id         *
        klist      {}
        name       T*
        parent     *
        pot        *::pot
        state      invalid
    }

    test constructor-1.2 {can set options} -setup {
        setup
    } -body {
        set T1 [pot new ::athena::tactic::GRANT -parent DUMMY]
        $T1 get parent
    } -cleanup {
        cleanup
    } -result {DUMMY}
    

    #-------------------------------------------------------------------
    # narrative

    test narrative-1.1 {narrative, default state} -setup {
        setup
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Grant actor ??? access to CAP ???.}

    test narrative-1.2 {narrative, with valid fields} -setup {
        setup
        $T1 configure \
            -klist {ABC CBS} \
            -alist [adb gofer make ACTORS BY_VALUE BOB]
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Grant actor BOB access to CAPs ABC and CBS.}

    #-------------------------------------------------------------------
    # check

    test check-1.1 {Initial state fails} -setup {
        setup
    } -body {
        ted pdicts [$T1 check]
    } -cleanup {
        cleanup
    } -match dict -result {
        alist {No rule specified}
        klist {No CAPs selected.}
    }

    test check-1.2 {non-existent caps} -setup {
        setup
    } -body {
        $T1 configure \
            -klist {ABS CBC} \
            -alist [adb gofer make ACTORS BY_VALUE BOB]
        $T1 check
    } -cleanup {
        cleanup
    } -result {klist {JOE does not own a CAP called "ABS".}}

    test check-1.3 {CAP not owned by non-existent caps} -setup {
        setup
    } -body {
        $T1 configure \
            -klist {CNN FOX} \
            -alist [adb gofer make ACTORS BY_VALUE BOB]
        $T1 check
    } -cleanup {
        cleanup
    } -result {klist {JOE does not own a CAP called "CNN".}}

    test check-1.4 {invalid alist gofer} -setup {
        setup
        $T1 configure \
            -klist {ABC CBS} \
            -alist {}
    } -body {
        $T1 check
    } -cleanup {
        cleanup
    } -result {alist {Not a gofer ACTORS value}}



    test check-2.1 {Success} -setup {
        setup
        $T1 configure \
            -klist {ABC CBS} \
            -alist [adb gofer make ACTORS BY_VALUE BOB]
    } -body {
        $T1 check
    } -cleanup {
        cleanup
    } -result {}

    #-------------------------------------------------------------------
    # obligate

    test obligate-1.1 {always succeeds.} -setup {
        setup
    } -body {
        $T1 obligate $coffer
    } -cleanup {
        cleanup
    } -result {1}

    #-------------------------------------------------------------------
    # execute

    test execute-1.1 {always succeeds} -setup {
        setup
        $T1 configure \
            -klist {ABC CBS} \
            -alist [adb gofer make ACTORS BY_VALUE BOB]
    } -body {
        $T1 obligate $coffer
        $T1 execute
        cap access save
        rdb eval {SELECT k FROM cap_access WHERE a='BOB' ORDER BY k}
    } -cleanup {
        cleanup
    } -result {ABC CBS CNN FOX PBS}

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::








