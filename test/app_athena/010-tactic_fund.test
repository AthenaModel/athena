# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-tactic_fund.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    tactic_fund(sim) tests.
#
#    This test suite tests the tactic_fund(sim) class.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    # If -valid is given, make T1 valid.
    proc setup {{opt ""}} {
        variable T1
        variable coffer
        ted create JOE BOB

        cash load
        set coffer [::athena::coffer new [::adb athenadb] JOE]

        set B1 [ted addblock JOE]
        set T1 [ted addtactic $B1 FUND]

        if {$opt eq "-valid"} {
            $T1 configure -a BOB
            $T1 check
        }
    }

    proc execute_setup {} {
        setup
        cash load
    }


    # Clean up after a test

    proc cleanup {} {
        # Clean up the test environment
        variable coffer
        $coffer destroy
        ted cleanup
    }

    #-------------------------------------------------------------------
    # Constructor

    test constructor-1.1 {default state} -setup {
        setup
    } -body {
        ted pdicts [$T1 getdict]
    } -cleanup {
        cleanup
    } -match dictglob -result {
        a          {}
        amount     0.0
        execstatus NONE
        faildict   {}
        id         *
        mode       ALL
        name       T*
        parent     *
        percent    0.0
        pot        *::pot
        state      invalid
    }

    test constructor-1.2 {can set options} -setup {
        setup -valid
    } -body {
        set T1 [ted newbean ::athena::tactic::FUND -a DUMMY]
        $T1 get a
    } -cleanup {
        cleanup
    } -result {DUMMY}

    #-------------------------------------------------------------------
    # narrative

    test narrative-1.1 {default state} -setup {
        setup
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Fund actor ??? with all remaining cash-on-hand each week.}

    test narrative-1.2 {mode EXACT} -setup {
        setup -valid
        $T1 configure -mode EXACT -amount 1000.0
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Fund actor {actor:BOB} with $1,000 each week.}

    test narrative-1.3 {mode UPTO} -setup {
        setup -valid
        $T1 configure -mode UPTO -amount 1000.0
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Fund actor {actor:BOB} with up to $1,000 each week.}

    test narrative-1.4 {mode PERCENT} -setup {
        setup -valid
        $T1 configure -mode PERCENT -percent 50.0
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Fund actor {actor:BOB} with 50.0% of cash-on-hand each week.}

    test narrative-1.5 {mode EXCESS} -setup {
        setup -valid
        $T1 configure -mode EXCESS -amount 1000.0
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Fund actor {actor:BOB} with any cash-on-hand over $1,000 each week.}

    #-------------------------------------------------------------------
    # check

    test check-1.1 {default state} -setup {
        setup
    } -body {
        ted pdicts [$T1 check]
    } -cleanup {
        cleanup
    } -match dict -result {
        a {No actor selected.}
    }

    test check-1.2 {no such actor} -setup {
        setup
        $T1 configure -a NONESUCH
    } -body {
        ted pdicts [$T1 check]
    } -cleanup {
        cleanup
    } -match dict -result {
        a {No such actor: "NONESUCH".}
    }

    test check-1.3 {check: OK} -setup {
        setup -valid
    } -body {
        $T1 check 
    } -cleanup {
        cleanup
    } -result {}


    #-------------------------------------------------------------------
    # obligate

    test obligate-1.1 {ALL} -setup {
        setup -valid
    } -body {
        set a [$T1 obligate $coffer]
        set b [$coffer cash]
        $T1 execute
        set c [rdb onecolumn {SELECT gifts FROM working_cash WHERE a='BOB'}]

        list $a $b $c
    } -cleanup {
        cleanup
    } -result {1 0.0 10000.0}

    test obligate-1.2 {EXACT, enough cash} -setup {
        setup -valid
        $T1 set mode EXACT
        $T1 set amount 1000.0
    } -body {
        set a [$T1 obligate $coffer]
        set b [$coffer cash]
        $T1 execute
        set c [rdb onecolumn {SELECT gifts FROM working_cash WHERE a='BOB'}]

        list $a $b $c
    } -cleanup {
        cleanup
    } -result {1 9000.0 1000.0}

    test obligate-1.3 {EXACT, not enough cash} -setup {
        setup -valid
        $T1 set mode EXACT
        $T1 set amount 10001.0
    } -body {
        $T1 obligate $coffer
        $T1 faildict
    } -cleanup {
        cleanup
    } -result {CASH {Required $10,001, but had only $10,000.}}

    test obligate-1.4 {UPTO, under cash-on-hand} -setup {
        setup -valid
        $T1 set mode UPTO
        $T1 set amount 9999.0
    } -body {
        set a [$T1 obligate $coffer]
        set b [$coffer cash]
        $T1 execute
        set c [rdb onecolumn {SELECT gifts FROM working_cash WHERE a='BOB'}]

        list $a $b $c
    } -cleanup {
        cleanup
    } -result {1 1.0 9999.0}

    test obligate-1.5 {UPTO, over cash-on-hand} -setup {
        setup -valid
        $T1 set mode UPTO
        $T1 set amount 10001.0
    } -body {
        set a [$T1 obligate $coffer]
        set b [$coffer cash]
        $T1 execute
        set c [rdb onecolumn {SELECT gifts FROM working_cash WHERE a='BOB'}]

        list $a $b $c
    } -cleanup {
        cleanup
    } -result {1 0.0 10000.0}

    test obligate-1.6 {PERCENT} -setup {
        setup -valid
        $T1 set mode PERCENT
        $T1 set percent 25.0
    } -body {
        set a [$T1 obligate $coffer]
        set b [$coffer cash]
        $T1 execute
        set c [rdb onecolumn {SELECT gifts FROM working_cash WHERE a='BOB'}]

        list $a $b $c
    } -cleanup {
        cleanup
    } -result {1 7500.0 2500.0}

    test obligate-1.7 {EXCESS, under cash-on-hand} -setup {
        setup -valid
        $T1 set mode EXCESS
        $T1 set amount 9999.0
    } -body {
        set a [$T1 obligate $coffer]
        set b [$coffer cash]
        $T1 execute
        set c [rdb onecolumn {SELECT gifts FROM working_cash WHERE a='BOB'}]

        list $a $b $c
    } -cleanup {
        cleanup
    } -result {1 9999.0 1.0}

    test obligate-1.8 {EXCESS, over cash-on-hand} -setup {
        setup -valid
        $T1 set mode EXCESS
        $T1 set amount 10001.0
    } -body {
        set a [$T1 obligate $coffer]
        set b [$coffer cash]
        $T1 execute
        set c [rdb onecolumn {SELECT gifts FROM working_cash WHERE a='BOB'}]

        list $a $b $c
    } -cleanup {
        cleanup
    } -result {1 10000.0 0.0}

    #-------------------------------------------------------------------
    # execute

    test execute-1.1 {default always succeeds} -setup {
        setup -valid
        $T1 obligate $coffer
    } -body {
        $T1 execute
        ted query {SELECT * FROM working_cash}
    } -cleanup {
        cleanup
    } -result {
a   cash_reserve income  cash_on_hand gifts   
--- ------------ ------- ------------ ------- 
JOE 200000.0     10000.0 0.0          0.0     
BOB 150000.0     5000.0  5000.0       10000.0 
    }

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::





