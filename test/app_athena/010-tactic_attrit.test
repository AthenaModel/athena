# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-tactic_attrit.test
#
# AUTHOR:
#    Dave Hanks
#
# DESCRIPTION:
#    tactic_attrit(sim) tests.
#
#    This test suite tests the tactic_attrit(sim) class.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    # If opt is -valid, sets up a valid tactic.
    proc setup {{opt ""}} {
        variable B1
        variable T1
        variable coffer

        # FIRST, create the basic entities.
        ted create JOE BLUE ALQ NB1 NB2 SUNN SHIA KURD USAID

        # NEXT, create a default ASSIGN tactic
        set B1 [ted addblock SYSTEM]
        set T1 [ted addtactic $B1 ATTRIT]

        # NEXT, prepare for obligation
        personnel start
        personnel load
        set coffer [coffer new JOE]
    }

    proc cleanup {} {
        variable coffer
        $coffer destroy
        strategy locking 0
        ted cleanup
    }

    #-------------------------------------------------------------------
    # Constructor

    test constructor-1.1 {default state} -setup {
        setup
    } -body {
        ted pdicts [$T1 getdict]
    } -cleanup {
        cleanup
    } -match dictglob -result {
        casualties 1
        execstatus NONE
        f          {}
        faildict   {}
        g1         NONE
        g2         NONE
        id         *
        mode       NBHOOD
        n          {}
        parent     *
        state      invalid
    }

    test constructor-1.2 {can set options} -setup {
        setup
    } -body {
        set T1 [tactic::ASSIGN new -parent DUMMY]
        $T1 get parent
    } -cleanup {
        cleanup
    } -result {DUMMY}

    #-------------------------------------------------------------------
    # narrative

    test narrative-1.1 {narrative, default state} -setup {
        setup
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Magic attrit 1 personnel in ???.}

    test narrative-1.2 {mode NBHOOD} -setup {
        setup
        $T1 configure -n NB1 -casualties 10
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Magic attrit 10 personnel in {nbhood:NB1}.}

    test narrative-1.3 {mode GROUP} -setup {
        setup
        $T1 configure -f BLUE -n NB1 -mode GROUP -casualties 10
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Magic attrit 10 of {group:BLUE}'s personnel in {nbhood:NB1}.}

    test narrative-1.4 {mode NBHOOD, responsible group} -setup {
        setup
        $T1 configure -n NB1 -casualties 10 -g1 BLUE
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Magic attrit 10 personnel in {nbhood:NB1} and attribute it to {group:BLUE}.}

    test narrative-1.5 {mode NBHOOD, two responsible groups} -setup {
        setup
        $T1 configure -n NB1 -casualties 10 -g1 BLUE -g2 ALQ
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Magic attrit 10 personnel in {nbhood:NB1} and attribute it to {group:BLUE} and {group:ALQ}.}

    test narrative-1.6 {mode GROUP, responsible group} -setup {
        setup
        $T1 configure -f SHIA -n NB1 -mode GROUP -casualties 10 -g1 BLUE
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Magic attrit 10 of {group:SHIA}'s personnel in {nbhood:NB1} and attribute it to {group:BLUE}.}

    test narrative-1.7 {mode GROUP, two responsible groups} -setup {
        setup
        $T1 configure -f SHIA -n NB1 -mode GROUP \
                      -casualties 10 -g1 BLUE -g2 ALQ
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Magic attrit 10 of {group:SHIA}'s personnel in {nbhood:NB1} and attribute it to {group:BLUE} and {group:ALQ}.}

    #-------------------------------------------------------------------
    # check

    test check-1.1 {Initial state fails} -setup {
        setup
    } -body {
        ted pdicts [$T1 check]
    } -cleanup {
        cleanup
    } -match dict -result {
        n {No neighborhood selected.} 
    }

    test check-1.2 {unknown n} -setup {
        setup 
    } -body {
        $T1 configure -n NONESUCH
        ted pdicts [$T1 check]
    } -cleanup {
        cleanup
    } -match dict -result {
        n {No such neighborhood: "NONESUCH".}
    }

    test check-1.3 {f doesn't exist} -setup {
        setup 
    } -body {
        $T1 configure -mode GROUP -n NB1 -f NONESUCH
        ted pdicts [$T1 check]
    } -cleanup {
        cleanup
    } -match dict -result {
        f {No such group: "NONESUCH".}
    }

    test check-1.4 {unknown g1} -setup {
        setup
    } -body {
        $T1 configure -f SHIA -n NB1 -g1 NONESUCH
        ted pdicts [$T1 check]
    } -cleanup {
        cleanup
    } -match dict -result {
        g1 {No such FRC group: "NONESUCH".}
    }

    test check-1.5 {unknown g2} -setup {
        setup
    } -body {
        $T1 configure -f SHIA -n NB1 -g1 BLUE -g2 NONESUCH
        ted pdicts [$T1 check]
    } -cleanup {
        cleanup
    } -match dict -result {
        g2 {No such FRC group: "NONESUCH".}
    }

    test check-1.6 {Success} -setup {
        setup
        $T1 configure -f SHIA -n NB1 -g2 BLUE -g2 ALQ
    } -body {
        $T1 check
    } -cleanup {
        cleanup
    } -result {}

    #-------------------------------------------------------------------
    # obligate
    #

    test obligate-1.1 {obligate always works, no money} -setup {
        setup
        $T1 configure -f SHIA -n NB1 -g2 BLUE -g2 ALQ
    } -body {
        $T1 obligate $coffer
    } -cleanup {
        cleanup
    } -result {1}

    #-------------------------------------------------------------------
    # execute

    test execute-1.1 {sets up casualties} -setup {
        setup
        $T1 configure -mode NBHOOD -n NB1 -g1 BLUE -g2 ALQ
    } -body {
        $T1 execute
        ted query {SELECT * FROM magic_attrit}
    } -cleanup {
        cleanup
    } -match glob -result {
id mode   n   f casualties g1   g2  
-- ------ --- - ---------- ---- --- 
1  NBHOOD NB1   1          BLUE ALQ 
    }

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::









