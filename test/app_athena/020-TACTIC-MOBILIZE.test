# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    020-TACTIC-MOBILIZE.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    TACTIC:MOBILIZE:* Orders tests
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    proc setup {} {
        ted create JOE BLUE ALQ
        variable S1
        variable B1
        variable T1
        set S1 [strategy getname JOE]
        ted order STRATEGY:BLOCK:ADD agent JOE
        lassign [$S1 blocks] B1
        ted order BLOCK:TACTIC:ADD block_id [$B1 id] typename MOBILIZE
        lassign [$B1 tactics] T1
    }

    # Clean up after a test

    proc cleanup {} {
        ted cleanup
    }

    #-------------------------------------------------------------------
    # TACTIC:MOBILIZE

    test TACTIC:MOBILIZE-1.1 {required parameters} -setup {
        setup
    } -body {
        ted order -reject TACTIC:MOBILIZE
    } -cleanup {
        cleanup
    } -result {
        tactic_id {required value}
    }

    test TACTIC:MOBILIZE-1.2 {unknown tactic_id} -setup {
        setup
    } -body {
        ted order -reject TACTIC:MOBILIZE tactic_id NONESUCH
    } -cleanup {
        cleanup
    } -match glob -result {
        tactic_id {Invalid MOBILIZE ID: "NONESUCH"}
    }

    test TACTIC:MOBILIZE-1.3 {invalid mode} -setup {
        setup
    } -body {
        ted order -reject TACTIC:MOBILIZE tactic_id [$T1 id] mode NONESUCH
    } -cleanup {
        cleanup
    } -result {
        mode {invalid value "NONESUCH", should be one of: ADD, PERCENT, UPTO, ENSURE}
    }

    test TACTIC:MOBILIZE-1.4 {invalid personnel} -setup {
        setup
    } -body {
        ted order -reject TACTIC:MOBILIZE tactic_id [$T1 id] personnel NONESUCH
    } -cleanup {
        cleanup
    } -result {
        personnel {invalid value "NONESUCH", expected integer}
    }

    test TACTIC:MOBILIZE-1.5 {invalid percent} -setup {
        setup
    } -body {
        ted order -reject TACTIC:MOBILIZE tactic_id [$T1 id] percent NONESUCH
    } -cleanup {
        cleanup
    } -result {
        percent {invalid value "NONESUCH", expected double}
    }

    test TACTIC:MOBILIZE-1.6 {0 personnel for mode} -setup {
        setup
    } -body {
        ted order -reject TACTIC:MOBILIZE tactic_id [$T1 id] \
            mode ADD personnel 0
    } -cleanup {
        cleanup
    } -result {
        personnel {Mode requires personnel greater than 0.}
    }

    test TACTIC:MOBILIZE-1.7 {0 percent for mode} -setup {
        setup
    } -body {
        # percent is initially 0.  mode PERCENT requires positive percent
        # on cross-check.
        ted order -reject TACTIC:MOBILIZE tactic_id [$T1 id] mode PERCENT
    } -cleanup {
        cleanup
    } -result {
        percent {Mode requires a percentage greater than 0.0%.}
    }

    test TACTIC:MOBILIZE-2.1 {tactic updated} -setup {
        setup
    } -body {
        ted order TACTIC:MOBILIZE \
            tactic_id [$T1 id] \
            g         BLUE     \
            mode      UPTO     \
            personnel 1000     \
            percent   5.0

        ted pdicts [$T1 getdict]
    } -cleanup {
        cleanup
    } -match dictglob -result {
        execstatus NONE
        faildict   {}
        g          BLUE
        id         *
        mode       UPTO
        parent     *
        percent    5.0
        personnel  1000
        pot        ::pot
        state      normal
    }

    test TACTIC:MOBILIZE-2.2 {notifier event} -setup {
        setup
    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        ted order TACTIC:MOBILIZE tactic_id [$T1 id] personnel 100
        ted notifier received
    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}

    test TACTIC:MOBILIZE-2.3 {undo notifier event} -setup {
        setup
        ted order TACTIC:MOBILIZE tactic_id [$T1 id] personnel 100
    } -body {
        ted notifier bind ::projectlib::bean <Monitor>
        cif undo -test
        ted notifier received
    } -cleanup {
        cleanup
    } -match glob -result {*{::projectlib::bean <Monitor>}*}
 
    test TACTIC:MOBILIZE-3.1 {identity} -setup {
        setup
    } -body {
        ted tactic identity $T1
    } -cleanup {
        cleanup
    } -result {OK}

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::







