# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    010-tactic_deposit.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    tactic_deposit(sim) tests.
#
#    This test suite tests the tactic_deposit(sim) class.
#
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Initialize tcltest(n) and athena(1)

if {[lsearch [namespace children] ::tcltest] == -1} {
    source [file join [file dirname [file normalize [info script]]] ted.tcl]

    # Initialize tcltest(n), app_athena(n), et al.
    ted init $argv
}

#-----------------------------------------------------------------------
# Test Suite
#
# The tests run in a namespace so as not to interfere with other
# test suites.

namespace eval ::athena_test::tests:: {
    #-------------------------------------------------------------------
    # Set up the test environment

    # Import tcltest(n)
    namespace import ::tcltest::*

    # Set up for tests

    proc setup {} {
        variable T1
        variable coffer
        ted create JOE BOB NB1 NB2 BLUE BRIT SHIA SUNN KURD
        cash load
        set coffer [coffer new JOE]


        set B1 [ted addblock JOE]
        set T1 [ted addtactic $B1 DEPOSIT]
    }

    # Clean up after a test

    proc cleanup {} {
        variable coffer
        $coffer destroy
        ted cleanup
    }

    #-------------------------------------------------------------------
    # Constructor

    test constructor-1.1 {default state} -setup {
        setup
    } -body {
        ted pdicts [$T1 getdict]
    } -cleanup {
        cleanup
    } -match dictglob -result {
        amount     0.0
        execstatus NONE
        faildict   {}
        id         *
        mode       ALL
        name       T*
        parent     *
        percent    0.0
        pot        *::pot
        state      normal
    }

     test constructor-1.2 {can set options} -setup {
        setup
    } -body {
        set T1 [::athena::tactic::DEPOSIT new -parent DUMMY]
        $T1 get parent
    } -cleanup {
        cleanup
    } -result {DUMMY}
   

    #-------------------------------------------------------------------
    # narrative

    test narrative-1.1 {narrative, default state} -setup {
        setup
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Deposit all cash-on-hand to cash reserve.}

    test narrative-1.2 {narrative, with amount} -setup {
        setup
        $T1 set mode EXACT
        $T1 set amount 10.0
    } -body {
        $T1 narrative
    } -cleanup {
        cleanup
    } -result {Deposit $10.00 to cash reserve.}

    #-------------------------------------------------------------------
    # check

    test check-1.1 {Initial state succeeds} -setup {
        setup
    } -body {
        $T1 check
    } -cleanup {
        cleanup
    } -result {}

    #-------------------------------------------------------------------
    # obligate

    test obligate-1.1 {Sufficient cash} -setup {
        setup
    } -body {
        $T1 set mode EXACT
        $T1 set amount 100.0

        set a [$T1 obligate $coffer]
        set b [$coffer cash]
        set c [$coffer reserve]

        list $a $b $c
    } -cleanup {
        cleanup
    } -result {1 9900.0 200100.0}

    test obligate-1.2 {Insufficient cash} -setup {
        setup
    } -body {
        $T1 set mode EXACT
        $T1 set amount 10001.0

        $T1 obligate $coffer
        $T1 faildict
    } -cleanup {
        cleanup
    } -result {CASH {Required $10,001, but had only $10,000.}}

    test obligate-1.3 {no cash} -setup {
        setup
    } -body {
        $coffer spend [$coffer cash]
        $T1 set mode EXACT
        $T1 set amount 1

        $T1 obligate $coffer
        $T1 faildict
    } -cleanup {
        cleanup
    } -result {CASH {Required $1, but had only $0.}}

    test obligate-1.4 {all cash} -setup {
        setup
    } -body {
        $T1 set mode ALL

        set a [$T1 obligate $coffer]
        set b [$coffer cash]
        set c [$coffer reserve]

        list $a $b $c
    } -cleanup {
        cleanup
    } -result {1 0.0 210000.0}

    test obligate-1.5 {upto cash (> on hand)} -setup {
        setup
    } -body {
        $T1 set mode UPTO
        $T1 set amount 10001.0

        set a [$T1 obligate $coffer]
        set b [$coffer cash]
        set c [$coffer reserve]

        list $a $b $c
    } -cleanup {
        cleanup
    } -result {1 0.0 210000.0}

    test obligate-1.6 {upto cash (< on hand)} -setup {
        setup
    } -body {
        $T1 set mode UPTO
        $T1 set amount 9000.0

        set a [$T1 obligate $coffer]
        set b [$coffer cash]
        set c [$coffer reserve]

        list $a $b $c
    } -cleanup {
        cleanup
    } -result {1 1000.0 209000.0}

    test obligate-1.7 {excess of cash} -setup {
        setup
    } -body {
        $T1 set mode EXCESS
        $T1 set amount 1000.0

        set a [$T1 obligate $coffer]
        set b [$coffer cash]
        set c [$coffer reserve]

        list $a $b $c
    } -cleanup {
        cleanup
    } -result {1 1000.0 209000.0}

    test obligate-1.8 {percentage of cash} -setup {
        setup
    } -body {
        $T1 set mode PERCENT
        $T1 set percent 10.0

        set a [$T1 obligate $coffer]
        set b [$coffer cash]
        set c [$coffer reserve]

        list $a $b $c
    } -cleanup {
        cleanup
    } -result {1 9000.0 201000.0}

    #-------------------------------------------------------------------
    # execute

    test execute-1.1 {default always succeeds} -setup {
        setup
        $T1 obligate $coffer
    } -body {
        $T1 execute
        ted query {SELECT * FROM working_cash WHERE a='JOE'}
    } -cleanup {
        cleanup
    } -result {
a   cash_reserve income  cash_on_hand gifts 
--- ------------ ------- ------------ ----- 
JOE 210000.0     10000.0 0.0          0.0   
    }

    #-------------------------------------------------------------------
    # Cleanup

    cleanupTests
}

namespace delete ::athena_test::tests::








